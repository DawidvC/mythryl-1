api X86REWRITE_PSEUDO {
  package f:  FLOWGRAPH

    # Takes a cluster and returns a range of registers to prohibit 
    # from spilling.  The arguments are:
    # 1. The first pseudo register
    # 2. The regmap before RA32.  If this is guaranteed to be 
    #    an identity you can use the identity function.   
    #    I use i::C.lookup regmap.
    #    
    # 3. The cluster.
    #
    # It returns a range of registers.
    #
    # NOTE: this version does not assume that the original regmap
    #       is an identity.  So there is some ugly regmap business to
    #       take care of.

    
  my rewrite:  
      { firstPseudo:     f::I.c::cell,
        originalRegmap:  f::I.c::cell -> f::I.c::cell,
        pruneCellSets:   Bool # should we remove references to memory 
                              # registers from all cell sets?
      } -> f::cluster -> f::I.c::cell * f::I.c::cell
end




###                  "I'm one of the most durable and fervent advocates
###                   of space exploration, but my take is that we could
###                   do it robotically at far less cost and far greater
###                   quantity and quality of results."
###
###                                         -- James Van Allen




generic X86RewritePseudo
  (package instruction:  X86INSTR
   package flowgraph:  FLOWGRAPH where I = Instr
   my ea:  Int -> Instr::operand) : X86REWRITE_PSEUDO =
pkg
  package c = X86Cells
  package i = Instr
  package f = flowgraph

  fun error msg = lowcode_error_message::error("X86RewritePseudo", msg)

  fun rewrite { firstPseudo, originalRegmap, pruneCellSets } 
              (f::CLUSTER { blocks, regmap, ... } ) = 
  let
    first = c::new_reg()
    lookup = c::lookup regmap
    fun shuffle (dests, srcs, tmp)  = let
      fun move (rd, rs) = i::MOVE { mvOp=i::MOVL, src=rs, dst=rd }
      fun loop((p as (rd, dst, rs, src)) . rest, changed, used, done, instrs) = 
	  if list::exists (fn (r:  i::C.cell) => dst==r) used then
	    loop (rest, changed, used, p . done, instrs)
	  else 
	    loop (rest, TRUE, used, done, move (i::DIRECT rd, i::DIRECT rs) . instrs)
	| loop([], FALSE, _, done, instrs) = (done, instrs)
	| loop([], TRUE, _, done, instrs) = 
	    loop (done, FALSE, map #4 done, [], instrs)

      fun cycle([], instrs) = instrs
	| cycle (moves, instrs) = 
	  (case loop (moves, FALSE, map #4 moves, [], instrs) 
	   of ([], instrs) => instrs
	    | ((rd, rd', rs, rs') . nonCyclic, instrs) => let
		my THE tmpR = tmp
		instrs' = move (tmpR, i::DIRECT rs) . instrs
		my (cyclic, instrs'') = 
		  loop (nonCyclic, FALSE, map #4 nonCyclic, [], instrs')
	      in cycle (cyclic, move (i::DIRECT rd, null_or::the tmp) . instrs'')
	      end
	  /*esac*/)
      fun rmCoalesced([], [], remain, coalesced) = (remain, coalesced)
	| rmCoalesced (rd . rds, rs . rss, remain, coalesced) = let
	    dst = lookup rd
	    src = lookup rs
	  in
	    if dst == -1 then #  eliminate dead copies 
	      rmCoalesced (rds, rss, remain, coalesced)
            else if dst == src then 
	      rmCoalesced (rds, rss, remain, 
                          move (i::DIRECT rd, i::DIRECT rs) . coalesced)
	    else rmCoalesced (rds, rss, (rd, dst, rs, src) . remain, coalesced)
	  end
    in reverse (cycle (rmCoalesced (dests, srcs, [], [])))
    end

    fun doBlock (f::BBLOCK { blknum, instructions, liveOut, next, ... } ) = let
	  fun pseudoR r = (r >= 8 and r < firstPseudo)
	  fun resetLiveOut() = let
	    fun reset (gp, fp, cc) = 
	      liveOut := (list::filter (not o pseudoR) gp, fp, cc)
	  in
	    case *next
	     of [] => reset *liveOut
	      | [(f::EXIT _, _)] => reset(*liveOut)
	      | _ => ()
	  end

	  #  subst: hd (acc) is the last instruction in the stream. 
	  fun subst (instruction, acc) = let
            fun mark (i,[]) = i
              | mark (instruction, note . notes) = mark (i::NOTE { instruction, note }, notes)

	    fun movl { src, dst, acc } = 
	        i::MOVE { mvOp=i::MOVL, src=src, dst=dst } . acc

	    fun displace (base, disp, acc, mem) = 
            let base' = originalRegmap base
            in  if pseudoR base' then 
	        let tmpR = c::new_reg()
		    newDisp = i::DISPLACE { base=tmpR, disp=disp, mem=mem }
  		in (newDisp, movl { src=ea base', dst=i::DIRECT tmpR, acc=acc } )
		end
 	        else (i::DISPLACE { base=base, disp=disp, mem=mem }, acc)
            end

	    fun indexedEa (base, index, scale, disp, mem) = 
	      i::Indexed { base=base, index=index, scale=scale, disp=disp, mem=mem }

	    fun indexed (NULL, index, scale, disp, acc, mem) = 
                let index' = originalRegmap index
                in  if pseudoR index' then 
		    let tmpR = c::new_reg()
		        newIndex = indexedEa (NULL, tmpR, scale, disp, mem)
		    in (newIndex, movl { src=ea index', dst=i::DIRECT tmpR, acc=acc } )
		    end
		    else (indexedEa (NULL, index, scale, disp, mem), acc)
                end
	      | indexed (ba as THE base, index, scale, disp, acc, mem) = 
                let base'  = originalRegmap base
                    index' = originalRegmap index
		    b = pseudoR base'
		    i = pseudoR index'
		in  if b and i then 
                    let tmpB = c::new_reg()
		        tmpI = c::new_reg()
		        operand = indexedEa (THE tmpB, tmpI, scale, disp, mem)
		    in (operand, movl { src=ea base', dst=i::DIRECT tmpB, 
				   acc=movl { src=ea index', 
                                            dst=i::DIRECT tmpI, acc=acc }} )
		    end
		  else if b then let
		      tmpB = c::new_reg()
		    in (indexedEa (THE tmpB, index, scale, disp, mem), 
			movl { src=ea base', dst=i::DIRECT tmpB, acc=acc } )
		    end
		  else if i then let
		      tmpI = c::new_reg()
		    in (indexedEa (ba, tmpI, scale, disp, mem), 
			movl { src=ea index', dst=i::DIRECT tmpI, acc=acc } )
		    end
		  else (indexedEa (ba, index, scale, disp, mem), acc)

		end
	    fun direct (r, acc) = 
            let r' = originalRegmap r
            in  if pseudoR r' then (ea r', acc) else (i::DIRECT r, acc) 
            end

	    fun operand (i::DIRECT r, acc) = direct (r, acc)
	      | operand (i::Indexed { base, index, scale, disp, mem }, acc) = 
		 indexed (base, index, scale, disp, acc, mem)
	      | operand (i::DISPLACE { base, disp, mem }, acc) = 
                 displace (base, disp, acc, mem)
	      | operand arg = arg

	    fun done (operand, f, an) = 
	    let my (operand', acc') = operand (operand, acc)
	    in  mark (f operand', an) . acc'
	    end

	    fun memArg (i::DISPLACE _) = TRUE
	      | memArg (i::Indexed _) = TRUE
	      | memArg (i::MemReg _) = TRUE
	      | memArg (i::LabelEA _) = TRUE
	      | memArg _ = FALSE

	    fun withTmp f =
            let t = c::new_reg()
	    in  f t
	    end

            fun rewriteCmpTest (cmptest, lsrc, rsrc, an) =
            let my (lsrcOpnd, acc1) = operand (lsrc, acc)
                my (rsrcOpnd, acc2) = operand (rsrc, acc1)
	    in  if memArg lsrcOpnd and memArg rsrcOpnd then 
		    withTmp (fn t =>
		       mark (cmptest { lsrc=i::DIRECT t, rsrc=rsrcOpnd }, an) .
			 movl { src=lsrcOpnd, dst=i::DIRECT t, acc=acc2 } )
		else
		    mark (cmptest { lsrc=lsrcOpnd, rsrc=rsrcOpnd }, an) . acc2
	    end

            fun rewrite (instruction, an) =
	    case instruction
	     of i::JMP (operand, labs) => done (operand, fn operand => i::JMP (operand, labs), an)
	      | i::JCC { operand, cond } => 
                  done (operand, fn operand => i::JCC { operand=operand, cond=cond }, an)
	      | i::MOVE { src, dst, mvOp } => let
		  my (srcOpnd, acc1) = operand (src, acc)
		  my (dstOpnd, acc2) = operand (dst, acc1)
		in
		  if memArg srcOpnd and memArg dstOpnd then 
		    withTmp (fn t =>
		       mark (i::MOVE { src=i::DIRECT t, dst=dstOpnd, mvOp=mvOp }, an) .
			    movl { src=srcOpnd, dst=i::DIRECT t, acc=acc2 } )
		  else 
		    mark (i::MOVE { src=srcOpnd, dst=dstOpnd, mvOp=mvOp }, an) . acc2
		end
	      | i::LEA { r32, address } => let
		  my (srcOpnd, acc1) = operand (address, acc)
                  r32' = originalRegmap r32
		in
		  if pseudoR r32' then 
		    withTmp (fn t =>
		       movl { dst=ea r32', src=i::DIRECT t, 
		 	    acc=mark (i::LEA { r32=t, address=srcOpnd }, an) . acc1 } )
		  else mark (i::LEA { r32=r32, address=srcOpnd }, an) . acc1
		end
	      | i::CMPL { lsrc, rsrc } => rewriteCmpTest (i::CMPL, lsrc, rsrc, an)
	      | i::CMPW { lsrc, rsrc } => rewriteCmpTest (i::CMPW, lsrc, rsrc, an)
	      | i::CMPB { lsrc, rsrc } => rewriteCmpTest (i::CMPB, lsrc, rsrc, an)
	      | i::TESTL { lsrc, rsrc } => rewriteCmpTest (i::TESTL, lsrc, rsrc, an)
	      | i::TESTW { lsrc, rsrc } => rewriteCmpTest (i::TESTW, lsrc, rsrc, an)
	      | i::TESTB { lsrc, rsrc } => rewriteCmpTest (i::TESTB, lsrc, rsrc, an)
	      | i::BINARY { binOp, src, dst } => let
		  my (srcOpnd, acc1) = operand (src, acc)
		  my (dstOpnd, acc2) = operand (dst, acc1)
		in 
		  if memArg srcOpnd and memArg dstOpnd then
		    withTmp (fn t =>
	             mark (i::BINARY { binOp=binOp, src=i::DIRECT t, dst=dstOpnd }, an) .
			 movl { src=srcOpnd, dst=i::DIRECT t, acc=acc2 } )
		  else 
                    mark (i::BINARY { binOp=binOp, src=srcOpnd, dst=dstOpnd }, an) . acc2
		end
	      | i::CALL (operand, def, uses, mem) => let
		  my (operand1, acc1) = operand (operand, acc)
		  fun cellset (gp, fp, cc) =
                    if pruneCellSets then
		      (list::filter (not o pseudoR) gp, fp, cc)
                    else
                      (gp, fp, cc)
		in mark (i::CALL (operand1, cellset def, cellset uses, mem), an) . acc1
		end
	      | i::MULTDIV { multDivOp, src } => 
		  done (src, 
                       fn operand => i::MULTDIV { multDivOp=multDivOp, src=operand }, an)
	      | i::MUL3 { dst, src1, src2 } =>  let
		  my (src1Opnd, acc1) = operand (src1, acc)
                  dst' = originalRegmap dst
		in
		  if pseudoR dst' then
		    withTmp (fn t =>
		      movl { dst=ea dst', src=i::DIRECT t, acc=
			mark (i::MUL3 { dst=t, src1=src1Opnd, src2=src2 }, an) . acc1 } )
		  else mark (i::MUL3 { dst=dst, src1=src1Opnd, src2=src2 }, an) . acc1
		end
	      | i::UNARY { unOp, operand } => 
		  done (operand, fn operand => i::UNARY { unOp=unOp, operand=operand }, an)
	      | i::SET { cond, operand } => 
		  done (operand, fn operand => i::SET { cond=cond, operand=operand }, an)
	      | i::PUSHL operand => done (operand, i::PUSHL, an)	
	      | i::PUSHW operand => done (operand, i::PUSHW, an)	
	      | i::PUSHB operand => done (operand, i::PUSHB, an)	
	      | i::POP operand => done (operand, i::POP, an)
	      | i::CMOV { cond, src, dst } => 
                  let my (srcOpnd, acc1) = operand (src, acc)
                      dst' = originalRegmap dst
                  in  if pseudoR dst then
		        withTmp (fn t =>
		         movl { dst=ea dst', src=i::DIRECT t, acc=
                           mark (i::CMOV { cond=cond, dst=t, src=srcOpnd }, an) .
                             acc1 } )
		      else 
                        mark (i::CMOV { cond=cond, dst=dst, src=srcOpnd }, an) . acc1
                  end
	      | i::COPY { dst, src, tmp } => let
		  # Note:
		  #  Parallel copies are not allowed after this point.
		  #  Consider:
		  #    (r8, r9, edx) <- (566, 567, 560)
		  #
		  #  RA32 may well decide to allocate 560 to r8.
		  #  After the rewrite we will get:
		  #
		  #      mem[r8] <- 566
		  #	  mem[r9] <- 567
		  #          edx <- 560
		  # 
		  # If 560 should spill, we all of a sudden have the 
		  # incorrect value being read from the spill location.
		  
		  fun f((instruction as i::MOVE { mvOp, src, dst } ) . rest, acc) =
		      (case (src, dst)
			of (i::DIRECT s, i::DIRECT d) =>
                        let d' = originalRegmap d   
                            s' = originalRegmap s
			in  if s'==d' then f (rest, acc) 
                            else if pseudoR d' and pseudoR s' then
			            f (rest, withTmp (fn t =>
				       (movl { src=i::DIRECT t, dst=ea d', 
                                           acc=movl { src=ea s', 
                                             dst=i::DIRECT t, acc=acc }} )))
			    else if pseudoR d' then
			      f (rest, withTmp (fn t =>
				 (movl { src=i::DIRECT s, dst=ea d', acc=acc } )))
			    else if pseudoR s' then
			      f (rest, withTmp (fn t =>
			         (movl { src=ea s', dst=i::DIRECT d, acc=acc } )))
			    else f (rest, i::COPY { src=[s], dst=[d], tmp=NULL } . acc)
                        end

		         | _  => f (rest, instruction . acc)
		      /*esac*/)

		    | f([], acc) = acc
	        in f (shuffle (dst, src, tmp), acc)
		end
	      | i::FSTPT operand => done (operand, i::FSTPT, an)
	      | i::FSTPL operand => done (operand, i::FSTPL, an)
	      | i::FSTPS operand => done (operand, i::FSTPS, an)
	      | i::FSTL operand => done (operand, i::FSTL, an)
	      | i::FSTS operand => done (operand, i::FSTS, an)
	      | i::FLDT operand => done (operand, i::FLDT, an)
	      | i::FLDL operand => done (operand, i::FLDL, an)
	      | i::FLDS operand => done (operand, i::FLDS, an)
	      | i::FILD operand => done (operand, i::FILD, an)
	      | i::FILDL operand => done (operand, i::FILDL, an)
	      | i::FILDLL operand => done (operand, i::FILDLL, an)
	      | i::FENV { fenvOp, operand } => done (operand, 
                    fn operand => i::FENV { fenvOp=fenvOp, operand=operand }, an)
	      | i::FBINARY { src, dst, binOp } => 
		  done (src, 
                       fn operand => i::FBINARY { binOp=binOp, src=operand, dst=dst }, an)
	      | i::FIBINARY { src, binOp } => 
		  done (src, fn operand => i::FIBINARY { binOp=binOp, src=operand }, an)
              | i::NOTE { instruction, note } => rewrite (instruction, note . notes)
	      | _ => mark (instruction, an) . acc
          in  rewrite (instruction,[])
	  end #  subst 
	in instructions := list::fold_left subst [] (reverse(*instructions));
	   if pruneCellSets then resetLiveOut() else ()
	end # DoBlock
      | doBlock _ = ()
  in apply doBlock blocks;  (first, c::new_reg())
  end #  rewrite 
end
