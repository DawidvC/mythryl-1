## seek.pkg
## (C) 1999 Lucent Technologies, Bell Laboratories
## Author: Matthias Blume (blume@kurims.kyoto-u.ac.jp)

# Compiled by:
#     src/app/make7/util/Make7_Utilities.make6


# Seek in an Input_Stream.


api Seek {

    exception UNABLE_TO_SEEK;

    seek:  (binary_io::Input_Stream,  file_position::Int)
           ->
           Void;
};

package seek : Seek {		# Seek	is from   src/app/make7/util/seek.pkg

    exception UNABLE_TO_SEEK;

    empty_vector
        =
        unt8_vector::from_list [];
    
    fun seek (stream, position)
        =
        {   fs =  binary_io::get_instream  stream;

	    my (reader, _)
                =
                binary_io::pure_io::get_reader  fs;

	    my binary_primitive_io::READER { set_position, ... }
                =
                reader;
	
	    case set_position
	      
	         NULL =>  raise exception UNABLE_TO_SEEK;

	         THE set_position
                     =>
		     {   set_position  position;

			 fs' =  binary_io::pure_io::make_instream (reader, empty_vector);

			 binary_io::set_instream (stream, fs');
		     };
            esac;
	};
};
