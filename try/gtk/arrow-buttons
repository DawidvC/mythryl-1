#!/usr/bin/mythryl

# This is a Mythryl version of the GTK+ tutorial
# arrow buttons example:
#
#     http://library.gnome.org/devel/gtk-tutorial/stable/x795.html
#

include gtk_via_pipe;

fun make_arrow_button (session, arrow_type, shadow_type)
    =
    {   button = make_button session;
	arrow  = make_arrow (session, arrow_type, shadow_type);

	add_kid { session, mom => button, kid => arrow  };

	show_widget (session, button);
	show_widget (session, arrow);

	button;
    };	

session = make_session [];

window = make_window  session;

set_window_title (session, window, "Arrow Buttons");

set_delete_event_callback session window .{ exit 0; };
set_destroy_callback      session window .{ exit 0; };

set_border_width (session, window, 10);

box = make_horizontal_box  (session, FALSE, 0);
set_border_width (session, box, 2);
add_kid { session, mom => window, kid => box  };

show_widget (session, box);

kid = make_arrow_button (session, ARROW_UP,    SHADOW_IN        );  pack_box { session, box, kid,  pack => FROM_START, expand => FALSE,  fill => FALSE, padding => 3 };
kid = make_arrow_button (session, ARROW_DOWN,  SHADOW_OUT       );  pack_box { session, box, kid,  pack => FROM_START, expand => FALSE,  fill => FALSE, padding => 3 };
kid = make_arrow_button (session, ARROW_LEFT,  SHADOW_ETCHED_IN );  pack_box { session, box, kid,  pack => FROM_START, expand => FALSE,  fill => FALSE, padding => 3 };
kid = make_arrow_button (session, ARROW_RIGHT, SHADOW_ETCHED_OUT);  pack_box { session, box, kid,  pack => FROM_START, expand => FALSE,  fill => FALSE, padding => 3 };

show_widget (session, window);

run_eventloop_indefinitely session;

exit 0;
