#  test the vtty widget 

# Compiled by:
#     src/lib/x-kit/tut/widget/widgets.make6

stipulate
    include threadkit;					# threadkit		is from   src/lib/src/lib/thread-kit/src/core-thread-kit/threadkit.pkg
    #
    package rx  =  run_xkit;				# run_xkit		is from   src/lib/x-kit/widget/lib/run-xkit.pkg
    package top =  topwindow;				# topwindow		is from   src/lib/x-kit/widget/basic/topwindow.pkg
    package w   =  widget;				# widget		is from   src/lib/x-kit/widget/basic/widget.pkg
    package wa  =  widget_attribute;			# widget_attribute	is from   src/lib/x-kit/widget/lib/widget-attribute.pkg
    package wy  =  widget_style;			# widget_style		is from   src/lib/x-kit/widget/lib/widget-style.pkg
    package vt  =  virtual_terminal;			# virtual_terminal	is from   src/lib/x-kit/widget/text/virtual-terminal.pkg
herein

    package test_vtty:  api {
			    do_it':  (List(String), String) -> Void;
			    do_it:    Void -> Void;
			    main:    (List(String), X) -> Void;
			}
    {
	resources = [
	    "*background: forestgreen"
	  ];

	fun tester root
	    =
	    {   top::start  topwindow;
		loop ();
	    }
	    where
		fun quit ()
		    =
		    {   w::delete_root_window root;
			shut_down_thread_scheduler 0;
		    };

		style = w::style_from_strings (root, resources);

		name = wy::make_view
			 { name    =>   wy::style_name [],
			   aliases => [ wy::style_name [] ]
			 };

		view = (name, style);

		vtty = vt::make_virtual_terminal root { rows => 24, cols => 80 };

		my (ins, outs)
		    =
		    vt::open_virtual_terminal vtty;

		topwindow_args
		    =
		    [ (wa::title,     wa::STRING_VAL "test"),
		      (wa::icon_name, wa::STRING_VAL "test")
		    ];

		topwindow = top::topwindow
			    (root, view, topwindow_args)
			    (vt::as_widget vtty);

		fun cat_file  fname
		    =
		    {   inf = file::open_for_read fname;

			fun out_f ()
			    = 
			    case (file::read_n (inf, 1024))
				#
				"" => ();
				s  => {  threadkit_file::write (outs, s);
					 out_f ();
				      };
			    esac;

			out_f ();

			file::close_input inf;
		    }
		    except
			io_exceptions::IO { name, function, ... }
			    =
			    threadkit_file::write (outs, (name + " " + function + "\n"));

		fun cat []    =>  threadkit_file::write (outs, "cat: missing file name\n");
		    cat files =>  apply cat_file files;
		end;	

		fun loop ()
		    =
		    for (;;) {

			threadkit_file::write (outs, "> ");
			threadkit_file::flush outs;

			line = threadkit_file::read_line ins;

			fun do_cmd ("cat"  ! t) =>  cat t;
			    do_cmd ("quit" ! _) =>  quit ();
			    #
			    do_cmd ("help" ! _) =>  threadkit_file::write (outs, "Commands: cat <files>, quit, help\n");
			    do_cmd (s      ! _) =>  threadkit_file::write (outs, "Unknown command: " + s + "\n");
			    #
			    do_cmd []           =>  ();
			end;

			do_cmd (string::tokens char::is_space (the line));
		    };

	    end;					# fun tester

	fun do_it' (debug_flags, server)
	    =
	    {   xtracing::init  debug_flags;

		rx::run_xkit'
		    #
		    tester
		    #
		    { display => THE server,
		      time_quantum_in_milliseconds => NULL
		    };
	    };

	fun do_it ()
	    =
	    rx::run_xkit  tester;

	fun main (program ! server ! _, _) =>  do_it' ([], server);
	    main _                         =>  do_it  ();
	end;
    };							# package test_vtty 
end;