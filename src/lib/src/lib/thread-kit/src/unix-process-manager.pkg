## unix-process-manager.pkg
## COPYRIGHT (c) 1989-1991 John H. Reppy

# Compiled by:
#     src/lib/std/stdlib.make6


# Unix process management.


package process_manager: (weak)  api {

    add_pid
	:
	posix::Process_Id
	->
	mailop::Mailop( posix::Exit_Status );

    poll_processes:  Void -> Void;

    any_waiting:  Void -> Bool;

}
{

    package p_proc = posix;			# posix				is from   src/lib/std/src/posix/posix.pkg
    package r      = threadkit_process_result;	# threadkit_process_result	is from   src/lib/thread-kit/src/util/threadkit-process-result.pkg
    package sched  = thread_scheduler;		# thread_scheduler		is from   src/lib/thread-kit/src/core-thread-kit/thread-scheduler.pkg

    Process_Id
        =
        PID  {
	    wait:  r::Threadkit_Process_Result( p_proc::Exit_Status ),
	    pid:   p_proc::Process_Id
        };

    waiting =  REF ([]: List( Process_Id ));

    fun add_pid pid
        =
        {   rv = r::make_threadkit_process_result ();
	  
	    waiting :=  PID { wait =>  rv,
			      pid
			    }
			!
			*waiting;

	    r::get_mailop  rv;
	};


    fun poll_processes ()
        =
	waiting
	    :=
	    list::filter
		poll_item
		*waiting
        where

	    # NOTE: It would be more efficient to
            #       poll for zombie processes
	    #       until there are no more.		XXX BUGGO FIXME

	    fun poll_pid  pid
		=
		p_proc::waitpid_nh (p_proc::W_CHILD pid, []);


	    fun poll_item (item as PID { wait, pid } )
		=
		case (poll_pid  pid)
		  
		    THE (_, status)
                        =>
                        {   sched::enqueue_tmp_thread
                                .{  r::put (wait, status);  };

		            FALSE;
                        };

		    NULL =>  TRUE;
                esac
		except
                    ex =
                        {   sched::enqueue_tmp_thread
                                .{  r::put_exception (wait, ex);  };

		            FALSE;
                        };
	end;


    fun any_waiting ()
        =
        case *waiting
	    [] =>  FALSE;
	    _  =>  TRUE;
        esac;

};



## COPYRIGHT (c) 1995 AT&T Bell Laboratories.
## Subsequent changes by Jeff Prothero Copyright (c) 2010,
## released under Gnu Public Licence version 3.
