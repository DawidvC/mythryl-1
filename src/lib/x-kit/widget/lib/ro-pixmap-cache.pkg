## ro-pixmap-cache.pkg
#
# Support for icons, button images
# and so forth:   Track what readonly
# windows we have on the X server and
# transparently load new ones as needed.
#

# Compiled by:
#     src/lib/x-kit/widget/xkit-widget.make6

###            "Opportunity is missed by most people
###              because it is dressed in overalls
###               and looks like work."
###
###                          -- Thomas Edison


stipulate
    include threadkit;								# threadkit			is from   src/lib/src/lib/thread-kit/src/core-thread-kit/threadkit.pkg
    #
    package bio =  bitmap_io;							# bitmap_io			is from   src/lib/x-kit/draw/bitmap-io.pkg
    package qk  =  quark;							# quark				is from   src/lib/x-kit/style/quark.pkg
    package xc  =  xcore;							# xcore				is from   src/lib/x-kit/xclient/xcore.pkg
herein

    package ro_pixmap_cache: (weak)  Ro_Pixmap_Cache {				# Ro_Pixmap_Cache		is from   src/lib/x-kit/widget/lib/ro-pixmap-cache.api

	exception BAD_NAME;

	Plea_Mail  = GET_RO_PIXMAP  String;
	Reply_Mail = Null_Or( xc::Ro_Pixmap );

	Ro_Pixmap_Cache
	    =
	    RO_PIXMAP_CACHE
	      { plea_slot:   Mailslot( Plea_Mail  ),
		reply_slot:  Mailslot( Reply_Mail )
	      };

										    # monomorphic_hash_table_g	is from   src/lib/src/monomorphic-hash-table-g.pkg
	package string_table
	    =
	    monomorphic_hash_table_g (
		#
		Hash_Key   = qk::Quark;
		same_key   = qk::same;
		hash_value = qk::hash;
	    );

	Window_Table
	    =
	    string_table::Hash_Table( xc::Ro_Pixmap );

	fun make_ro_pixmap_cache
	    ( screen,
	      name_to_cs_pixmap
	    )
	    =
	    {   exception NOT_FOUND;

		my window_table:  Window_Table
		   =
		   string_table::make_table (32, NOT_FOUND);

		note_window =  string_table::set   window_table;
		find_window =  string_table::find  window_table;

		# Parse file, being careful to close
		# it properly in an exception is raised:
		#
		fun parse_file (fd, parse)
		    = 
		    (parse fd
		     before
			 file::close_input  fd
		    ) 
		    except
			e = {   file::close_input  fd;
				raise exception e;
			    };

		fun make_window_from_file (name, quark)
		    =
		    {   file_name = substring (name, 1, size name - 1);

			fd = file::open_for_read  file_name;

			my { image, ... }
			    =
			    parse_file (fd, bio::read_bitmap);

			t = xc::make_ro_pixmap_from_cs_pixmap
				screen
				image;

			note_window (quark, t); THE t;
		    };

		fun make_window_from_cs_pixmap  quark
		    =
		    {   window
			    =
			    xc::make_ro_pixmap_from_cs_pixmap
				screen
				(name_to_cs_pixmap  quark);

			note_window (quark, window);

			THE window;
		    };

		fun make_window (arg as (name, q))
		    = 
		    if (string::get (name, 0) == '@')
			#
			make_window_from_file  arg;
		    else
			make_window_from_cs_pixmap  q;
		    fi
		    except _ = NULL;

								    # quark		is from   src/lib/x-kit/style/quark.pkg
		fun do_plea (GET_RO_PIXMAP name)
		    =
		    {   quark = qk::quark name;

			case (find_window quark)
			    #
			    NULL => make_window (name, quark);
			    s    => s;
			esac;
		    };

		plea_slot  =  make_mailslot ();
		reply_slot =  make_mailslot ();

		fun loop ()
		    =
		    for (;;) {
			#
			give  (reply_slot,   do_plea (take  plea_slot));
		    };

		xtracing::xspawn("ro_pixmap_cache", loop);

		RO_PIXMAP_CACHE { plea_slot, reply_slot };
	    };

	fun get_ro_pixmap
		(RO_PIXMAP_CACHE { plea_slot, reply_slot } )
		name
	    =
	    {   give  (plea_slot,  GET_RO_PIXMAP name);

		case (take  reply_slot)
		    #
		    THE s => s;
		    _     => raise exception BAD_NAME;
		esac;
	    };
    };

end;


##########################################################################
#   The following is support for outline-minor-mode in emacs.		 #
#  ^C @ ^T hides all Text. (Leaves all headings.)			 #
#  ^C @ ^A shows All of file.						 #
#  ^C @ ^Q Quickfolds entire file. (Leaves only top-level headings.)	 #
#  ^C @ ^I shows Immediate children of node.				 #
#  ^C @ ^S Shows all of a node.						 #
#  ^C @ ^D hiDes all of a node.						 #
#  ^HFoutline-mode gives more details.					 #
#  (Or do ^HI and read emacs:outline mode.)				 #
#									 #
# Local variables:							 #
# mode: outline-minor							 #
# outline-regexp: "[{ \t]*\\(fun \\)"			 		 #
# End:									 #
##########################################################################
