## splash.pkg

# Compiled by:
#     src/lib/x-kit/tut/add/add.make6

stipulate
    package xc =  xcore;					# xcore		is from   src/lib/x-kit/xclient/xcore.pkg
    package xd =  xdraw;					# xdraw		is from   src/lib/x-kit/xclient/xdraw.pkg
    package xg =  xgeometry;					# xgeometry	is from   src/lib/x-kit/xclient/xgeometry.pkg
    #
    package w  =  widget;					# widget	is from   src/lib/x-kit/widget/basic/widget.pkg
    #
    package im = images;					# images	is from   src/lib/x-kit/tut/add/images.pkg
herein

    package splash {

	stipulate

	    fun bbox []
		    =>
		    xg::BOX { col=>0, row=>0, wide=>0, high=>0 };

		bbox (xg::POINT { col=>x, row=>y } ! pts)
		    =>
		    bb (x, y, x, y, pts)
		    where
			fun bb (minx, miny, maxx, maxy,[])
				=> 
				xg::BOX { col=>minx, row=>miny, wide=>maxx-minx+1, high=>maxy-miny+1 };

			    bb (minx, miny, maxx, maxy, xg::POINT { col=>x, row=>y } ! pts)
				=>
				bb ( min (minx, x), min (miny, y),
				     max (maxx, x), max (maxy, y),
				     pts
				   );
			end;
		    end;
	    end;

	    fun make_splash (root, water_ro_pixmap)
		=
		mks
		where
		    screen =  w::screen_of  root;

		    water_pen
			=
			xd::make_pen [ xd::FILL_STYLE_STIPPLED, 
				       xd::STIPPLE     water_ro_pixmap,
				       xd::FOREGROUND  (xc::rgb8_from_int 1)
				     ];

		    fun mks pts
			=
			{   (bbox pts) ->  xg::BOX { col, row, wide, high };

			    del_pt = xg::POINT { col, row };

			    pts' =  map (fn p = xg::sub_pt (p, del_pt))
					pts;

			    pixmap =  xc::make_rw_pixmap  screen  (xg::SIZE { wide, high }, 1);

			    draww  =  xd::drawable_of_rw_pixmap  pixmap;

			    xd::clear_drawable draww;

			    xd::fill_polygon
				draww
				water_pen
				{ shape =>  xd::NONCONVEX_SHAPE,
				  verts =>  pts'
				};

			    ro_pixmap
                                =
                                xc::make_ro_pixmap_from_rw_pixmap
				       pixmap;

			    xc::destroy_rw_pixmap  pixmap;

			    { origin => head pts',  data => ro_pixmap }: im::Image;
			};
		end;


	    fun make_point (col, row)
		=
		xg::POINT { col, row => -row };


	    little_data
		=
		[ (  0,   0),
		  ( 10,  18),
		  (  6,  15),
		  (  4,  20),
		  (  0,  12),
		  ( -4,  18),
		  ( -7,  15),
		  (-15,  20)
		];

	    medium_data
		=
		[ (  0,   0),
		  ( 20,  30),
		  ( 14,  25),
		  ( 10,  20),
		  (  8,  24),
		  (  5,  21),
		  (  3,  27),
		  (  0,  22), 
		  ( -3,  18),
		  ( -5,  23),
		  ( -7,  25),
		  (-11,  20),
		  (-14,  24),
		  (-18,  21),
		  (-20,  25) 
		];

	    big_data
		=
		[ (  0,   0),
		  ( 30,  35),
		  ( 28,  38),
		  ( 25,  36),
		  ( 23,  32),
		  ( 20,  36),
		  ( 18,  34), 
		  ( 15,  31),
		  ( 13,  37),
		  ( 10,  32),
		  (  8,  34),
		  (  5,  31),
		  (  3,  37),
		  (  0,  32), 
		  ( -3,  28),
		  ( -5,  33),
		  ( -7,  35),
		  (-11,  30),
		  (-14,  34),
		  (-18,  31),
		  (-20,  35),
		  (-21,  30),
		  (-24,  34),
		  (-29,  31),
		  (-32,  37) 
		];

	    splash_list
		=
		map (fn pts = map make_point pts) 
		    [ little_data, medium_data, big_data];

	herein

	    fun make_splashes arg
		=
		map (make_splash arg)
		    splash_list;

	end;			# stipulate
    };

end;

## COPYRIGHT (c) 1996 AT&T Research.
## Subsequent changes by Jeff Prothero Copyright (c) 2010,
## released under Gnu Public Licence version 3.
