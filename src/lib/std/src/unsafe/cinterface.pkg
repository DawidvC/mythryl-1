## cinterface.pkg

# Compiled by:
#     src/lib/std/src/Base-imp.make6

stipulate
    package string =  string_guts;		# string_guts	is from   src/lib/std/src/string-guts.pkg
herein
    package c_interface: Cinterface {		# Cinterface	is from   src/lib/std/src/unsafe/cinterface.api

						# inline_t	is from   src/lib/core/init/built-in.pkg

						# for assembly see
                                                #     src/lib/core/init/core.pkg
                                                #     src/lib/core/init/runtime-system-placeholder.pkg
                                                #     src/runtime/machine-dependent/X86.prim.asm

	Cfunction = assembly::a::Cfunction;

	bind_cfun = assembly::a::bind_cfun;

	exception  CFUN_NOT_FOUND String;

	fun c_function module_name fun_name
	    =
	    {   cfun =  bind_cfun (module_name, fun_name);

		if   (inline_t::cast cfun  !=  0)
		    
		     fn x =  (assembly::a::callc (cfun, x));
		else
		     raise exception  CFUN_NOT_FOUND (string::cat [module_name, ".", fun_name]);
		fi;
	    };


	System_Const
            =
            ((Int, String));

	exception  SYSTEM_CONSTANT_NOT_FOUND String;

	fun find_sys_const (name, l)
	    =
	    get l
	    where
		fun get []
			=>
			NULL;

		    get ((sys_const:  System_Const) ! r)
			=>
			if   (#2 sys_const  ==  name)
			    
			     THE sys_const;
			else
			     get r;
			fi;
		end;
	    end;

	fun bind_sys_const (name, l)
	    =
	    case (find_sys_const (name, l))
	      
		 THE sc =>   sc;
		 NULL   =>   raise exception  SYSTEM_CONSTANT_NOT_FOUND name;
	    esac;

    }; #  package c_interface 
end;



## COPYRIGHT (c) 1994 AT&T Bell Laboratories.
## Subsequent changes by Jeff Prothero Copyright (c) 2010,
## released under Gnu Public Licence version 3.
