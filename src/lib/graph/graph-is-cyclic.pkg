#
# Tests if a graph is cyclic
#
# -- Allen Leung

# Compiled by:
#     src/lib/graph/graphs.make6

package graph_is_cyclic: (weak)  Graph_Is_Cyclic		# Graph_Is_Cyclic	is from   src/lib/graph/graph-is-cyclic.api
{
   package g= graph;	# graph	is from   src/lib/graph/graph.pkg

   exception CYCLIC;


   # Cyclic test

   fun is_cyclic (g::GRAPH ggg)
       = 
       {   nnn       = ggg.capacity (); 
	   visited = bit_set::create nnn;
	   done    = bit_set::create nnn;

	   fun dfs i
               =
	       if  (bit_set::mark_and_test (visited, i))

		    if  (not (bit_set::contains (done, i)))
                         raise exception CYCLIC;
                    fi;
	      else 
		    dfs_succ (ggg.out_edges i);
		    bit_set::set (done, i);
              fi

	   also
           fun dfs'(i, _)
               =
               dfs i

	   also
           fun dfs_succ [] =>   ();

	       dfs_succ((_, j, _) ! es)
                   =>
                   {   dfs j;
                       dfs_succ es;
                   };
           end;

	   { ggg.forall_nodes dfs'; FALSE;}
           except
               CYCLIC = TRUE;
       };
};

