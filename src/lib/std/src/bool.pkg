## bool.pkg

# Compiled by:
#     src/lib/std/src/stdlib-core.make6



###             A sage
###                who had filled his glass
###                    at the fountain of truth
###             Said, in
###                a statement
###                    that later became canonical
###             To his
###                disciples,
###                    patterns of eager youth:
###             "I have
###                seen truth itself --
###                    and it's conical."
###
###                              -- Piet Hein
###
### [ NB: Piet's illustration
###       shows the sage brandishing
###       an overflowing cone-shaped glass. ]



package bool: (weak) Bool {			# Bool			is from   src/lib/std/src/bool.api

    Bool == primitive_types::Bool;

						# inline_t		is from   src/lib/core/init/built-in.pkg	
    not = inline_t::inlnot;

    # NOTE: this should probably accept
    # a wider range of arguments, but the
    # spec hasn't been written yet.              XXX BUGGO FIXME
    #
    fun scan (getc:   number_string::Reader( Char, X ))  cs
        =
	case (getc (pre_basis::skip_ws  getc  cs))
          
	     THE ('T', cs')
                 =>
                 case (pre_basis::get_nchars getc (cs', 3))
                   
		      THE (['R', 'U', 'E'], cs'')
                          =>
                          THE (TRUE, cs'');

		      _   => NULL;
                 esac;

	     THE ('F', cs')
                 =>
                 case (pre_basis::get_nchars  getc  (cs', 4))
		   
                      THE (['A', 'L', 'S', 'E'], cs'')
                          =>
                          THE (FALSE, cs'');

		      _   => NULL;
                 esac;

	     _ => NULL;
        esac;

    fun to_string TRUE  =>  "TRUE";
        to_string FALSE =>  "FALSE";
    end;

    from_string
        =
        pre_basis::scan_string  scan;

};




## COPYRIGHT (c) 1995 AT&T Bell Laboratories.
## Subsequent changes by Jeff Prothero Copyright (c) 2010,
## released under Gnu Public Licence version 3.
