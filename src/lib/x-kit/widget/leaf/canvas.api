## canvas.api
#
#     "The canvas widget provides a drawing surface that
#      can also be used as a basis for building new widgets."
#        -- p24, Gansner+Reppy's 1993 eXene widget manual,
#           http:://mythryl.org/pub/exene/1993-widgets.ps 
#
# Use 'drawable_of' to get the actual X "drawable"
# associated with the canvas.  For the set of types and
# functions available for drawing on a drawable see:
#
#     src/lib/x-kit/xclient/xdraw.api
#
# Note that the X-server side canvas does not actually
# exist until the canvas is realized;  attempting
# to draw on it prior to that will block the thread.
#
# 

# Compiled by:
#     src/lib/x-kit/widget/xkit-widget.make6

# This api is implemented in:
#
#     src/lib/x-kit/widget/leaf/canvas.pkg

stipulate
    package xc =  xcore;		# xcore			is from   src/lib/x-kit/xclient/xcore.pkg
    package xd =  xdraw;		# xdraw			is from   src/lib/x-kit/xclient/xdraw.pkg
    package xg =  xgeometry;		# xgeometry		is from   src/lib/x-kit/xclient/xgeometry.pkg
    package xi =  xinput;		# xinput		is from   src/lib/x-kit/xclient/xinput.pkg
    #
    package w  =  widget;		# widget		is from   src/lib/x-kit/widget/basic/widget.pkg
herein

    api Canvas {

	Canvas;

	canvas
	    :
	    (w::Root_Window, w::View, List(w::Arg))
	    ->
	    w::Widget_Size_Preference
	    ->
	    (Canvas, xg::Size, xi::Kidplug);

	make_canvas
	    :
	    w::Root_Window
	    ->
	    w::Widget_Size_Preference
	    ->
	    ( Canvas,					# Canvas on which to draw.
	      xg::Size,					# Current size of canvas.
	      xi::Kidplug
	    );

	as_widget:    Canvas -> w::Widget;

	size_of:      Canvas -> xg::Size;
	    # 
	    # Return current size of canvas.  Since changes
	    # in canvas size are reported to the widget via
	    # X resize events, this call is rarely needed.

	drawable_of:  Canvas -> xd::Drawable_Window;
	    #
	    # The actual x-kit drawing commands all take
	    # a drawable as parameter, not a canvas.


	# Modify the attributes of the canvas 
	#
	set_background_color:  Canvas -> Null_Or( xc::Rgb ) -> Void;
	    #
	    # Set the background color attribute of the window.
	    #	
	    # This does not immediately affect the window's contents,
	    # but if it is done before the window is mapped the window
	    # will come up with the right color.


	set_cursor:  Canvas -> Null_Or( xc::Xcursor ) -> Void;

    };
end;

## COPYRIGHT (c) 1991 by AT&T Bell Laboratories  See COPYRIGHT file for details.
## Subsequent changes by Jeff Prothero Copyright (c) 2010,
## released under Gnu Public Licence version 3.
