/*
 *  Convert the old cluster format into the new control flow graph format
 *
 * -- Allen Leung
 */

api CLUSTER2CFG =
api
   package control_flow_graph:  Control_Flow_Graph
   package f:    FLOWGRAPH
      sharing control_flow_graph::I = f::I
      sharing control_flow_graph::P = f::P

   my cluster2cfg:  f::cluster -> control_flow_graph::cfg

end 

generic package Cluster2CFG
   (package control_flow_graph:        Control_Flow_Graph 
    package util:       CFG_UTIL
    package flowgraph:  FLOWGRAPH
    package instruction_properties:  Instruction_Properties
       sharing Util::control_flow_graph = control_flow_graph
       sharing control_flow_graph::I = flowgraph::I = instruction_properties::I
       sharing control_flow_graph::P = flowgraph::P
   ) : CLUSTER2CFG
{

    package control_flow_graph  = control_flow_graph
    package i    = control_flow_graph::I
    package f    = flowgraph
    package g    = graph
    package w    = control_flow_graph::W

    fun error msg = lowcode_error_message::error("Cluster2CFG", msg)

    fun cluster2cfg
         (f::CLUSTER { blocks, entry, exit, blkCounter, annotations, ... } )=
    let fun id (f::BBLOCK { blknum, ... } ) = blknum
          | id (f::ENTRY { blknum, ... } ) = blknum
          | id (f::EXIT { blknum, ... } )  = blknum
          | id _                      = error "id"

        fun idfreq (blk, freq) = (id blk, freq)

        fun first_block (f::BBLOCK { blknum, ... } . _) = blknum
          | first_block (_ . bs)                   = first_block bs
          | first_block []                        = error "first_block"

        reorder = REF FALSE
        info = control_flow_graph::INFO { first_block   = REF (first_block blocks),
                              reorder      = reorder,
                              annotations  = annotations
                             }

        my CFG as g::GRAPH cfg = control_flow_graph::cfg (info)

        my f::ENTRY { blknum = ENTRY, ... } = entry
        my f::EXIT { blknum = EXIT, ... }   = exit

            /* Add a list of blocks into the CFG */ 
        fun add (f::ENTRY e . rest, Ps, Ls)     = add_entry (e, Ps, Ls, rest)
          | add (f::EXIT e . rest, Ps, Ls)      = add_exit (e, Ps, Ls, rest)
          | add (f::BBLOCK b . rest, Ps, Ls)    = add_block (b, reverse Ps, Ls, rest)
          | add((f::PSEUDO p) . rest, Ps, Ls)  = 
              add (rest, control_flow_graph::PSEUDO p . map control_flow_graph::LABEL Ls@Ps,[])
          | add((f::LABEL l) . rest, Ps, Ls)   = add (rest, Ps, l . Ls)
          | add([], Ps, Ls)                  = finish (Ps, Ls)

            #  Insert an entry node 
        and add_entry( { blknum, next, freq }, [], [], rest) =
              ( cfg.add_node (blknum, control_flow_graph::newStart (blknum, freq));
                cfg.set_entries [blknum];
                apply
                     (fn (blk, w) =  add_edge (blknum, id blk, control_flow_graph::JUMP, w)) 
                     *next;
                add (rest, [], [])
              )
          | add_entry _ = error "add_entry"

            #  Insert an exit node 
        and add_exit( { blknum, prior, freq }, [], [], rest) = 
              ( cfg.add_node (blknum, control_flow_graph::newStop (blknum, freq));
                cfg.set_exits [blknum];
                add (rest, [], [])
              )
  
            #  Insert an normal basic block 
       and add_block( { blknum, annotations,
                      freq, liveIn, liveOut, next, prior, instructions },
                     Ps, Ls, rest) =
           let an = *annotations
               an = 
                   case *next of
                     ([(f::EXIT _, _)] | []) => control_flow_graph::LIVEOUT.create *liveOut . an
                   | _ => an
               bb = control_flow_graph::BLOCK { id    = blknum,
                                  kind  = control_flow_graph::NORMAL,
                                  labels= REF Ls,
                                  freq  = freq,
                                  data  = REF Ps,
                                  instructions = instructions,
                                  annotations=REF an
                                 }
           in  cfg.add_node (blknum, bb);
               add_edges (blknum, *next, *instructions);
               /*
               add_call_edges (blknum, *callSucc);
               add_return_edge (blknum, *returnSucc);
                */
               add (rest, [], [])
           end

            #  Finished insertion 
       and finish([],[]) = ()
         | finish (Ps,[]) = 
               let my control_flow_graph::BLOCK { data, labels, ... } = cfg.node_info EXIT
               in  data := Ps @ *data
               end
         | finish _ = error "finish"

            #  Insert one edge into the flowgraph 
       and add_edge (i, j, k, w) =
           let k = if ENTRY == i then control_flow_graph::ENTRY
                       else if EXIT == j then control_flow_graph::EXIT
                       else k
           in  cfg.add_edge (i, j, control_flow_graph::EDGE { k=k, w=w, a=REF [] } )
           end

            #  Add edges into the flowgraph 
       and add_edges (i, succs, instructions) = 
           let fun is_fallsthru (j, yes, no) =
                   if j == i + 1 then
                      (case instructions of
                         jmp . _ => (case instruction_properties::instrKind jmp of
                                      instruction_properties::IK_JUMP => no
                                    | _                 => yes
                                   )
                      |  _ => yes)
                   else no
               fun add_branch (i, (j, jw), (k, kw)) =
               let j = id j and k = id k
               in  if j == i + 1 then
                     ( add_edge (i, j, control_flow_graph::BRANCH FALSE, jw);
                       add_edge (i, k, control_flow_graph::BRANCH TRUE, kw))
                   else if k == i + 1 then
                     ( add_edge (i, k, control_flow_graph::BRANCH FALSE, kw);
                       add_edge (i, j, control_flow_graph::BRANCH TRUE, jw))
                   else error "add_branch"
               end
               fun add_switch (i, _,[]) = ()
                 | add_switch (i, k, (j, jw) . js) =
                   (add_edge (i, id j, control_flow_graph::SWITCH k, jw); add_switch (i, k+1, js))
           in  case succs of
                 []      => ()
               | [(j, w)] => 
                   let j = id j
                   in  add_edge (i, j, is_fallsthru (j, control_flow_graph::FALLSTHRU, control_flow_graph::JUMP), w)
                   end
               | [j, k] => add_branch (i, j, k)
               | js    => add_switch (i, 0, js)
           end

           fun check_for_bad_entries () =
               apply (fn (i, j, e) =>
                     if j == EXIT then ()
                     else
                     case cfg.in_edges j of 
                        [_] => () #  only edge from ENTRY, okay 
                     |  _ => 
                        error("entry "$int::to_string j$" has internal edges") 
                   ) (cfg.out_edges ENTRY)

           #  Add edge from entry to exit 
           fun insert_entry_to_exit () = 
               if cfg.has_edge (ENTRY, EXIT) then ()
               else add_edge (ENTRY, EXIT, control_flow_graph::JUMP, REF 0)
    in 
        add (entry . exit . blocks,[],[]);
        check_for_bad_entries();
        insert_entry_to_exit(); 
        CFG
    end

}

