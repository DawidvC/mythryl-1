## pulldown-menu-button.pkg

# Compiled by:
#     src/lib/x-kit/widget/xkit-widget.make6



###            "Truth is the daughter of time,
###              not of authority."
###
###                     -- Francis Bacon

stipulate
    include threadkit;						# threadkit			is from   src/lib/src/lib/thread-kit/src/core-thread-kit/threadkit.pkg
    #
    package pu =  popup_menu;					# popup_menu			is from   src/lib/x-kit/widget/composite/popup-menu.pkg
    package ts =  toggleswitches;				# toggleswitches		is from   src/lib/x-kit/widget/leaf/toggleswitches.pkg
    package wg =  widget;					# widget			is from   src/lib/x-kit/widget/basic/widget.pkg
    package wa =  widget_attribute;				# widget_attribute		is from   src/lib/x-kit/widget/lib/widget-attribute.pkg
    package wy =  widget_style;					# widget_style			is from   src/lib/x-kit/widget/lib/widget-style.pkg
    package xg =  xgeometry;					# xgeometry			is from   src/lib/x-kit/xclient/xgeometry.pkg
    package xi =  xinput;					# xinput			is from   src/lib/x-kit/xclient/xinput.pkg
herein

    package pulldown_menu_button: (weak)  Pulldown_Menu_Button {	# Pulldown_Menu_Button		is from   src/lib/x-kit/widget/menu/pulldown-menu-button.api

	fun make_pulldown_menu_button  root_window  (label, menu)
	    =
	    {   w_slot = make_mailslot ();
		r_slot = make_mailslot ();

		all_bttns = map  xi::MOUSEBUTTON  [1, 2, 3, 4, 5];

		name = wy::make_view { name    =>  wy::style_name ["menuButton"],
				       aliases =>  []
				     };

		args = [ (wa::relief, wa::RELIEF_VAL wg::FLAT),
			 (wa::label,  wa::STRING_VAL label)
		       ];

		bttn = ts::label_button
                         ( root_window,
                           ( name,
                             wg::style_of  root_window
                           ),
                           args
                         )
                         (fn _ = ());

		fun pre_fn  () =  ts::set_button_on_off_flag (bttn, TRUE);
		fun post_fn () =  ts::set_button_on_off_flag (bttn, FALSE);

		fun query arg
		    =
		    {   give (w_slot, arg);
			take r_slot;
		    };


		fun pos (pu::WHERE_INFO { screen_point=> xg::POINT { col=>sx, row=>sy },
					  window_point=> xg::POINT { col=>x, row=>y },
					  timestamp,
					  button
					},
			 xg::SIZE { high, ... }
			)
		    = 
		    pu::PUT_POPUP_MENU_UPPERLEFT_ON_SCREEN
                        (xg::POINT { col=>sx-x, row=>sy-y+high+1 } );


		my (widget, mailop)
		    = 
		    pu::attach_positioned_menu_to_widget (ts::as_widget bttn, all_bttns, menu, query);


		fun menu_realize { window, size, kidplug }
		    =
		    {   kidplug ->  xi::KIDPLUG { from_mouse', from_other', ... };

			m_slot = make_mailslot ();
			c_slot = make_mailslot ();

			fun do_mouse  envelope
			    =
			    {   case (xi::envelope_contents  envelope)
				    #                              
				    xi::MOUSE_FIRST_DOWN _ => pre_fn  ();
				    xi::MOUSE_LAST_UP    _ => post_fn ();
				    _                      => ();
				esac;

				give (m_slot, envelope);
			    };

			fun do_mom (envelope, size)
			    =
			    case (xi::envelope_contents  envelope)
				#
				xi::ETC_RESIZE (xg::BOX { wide, high, ... } )
				    => 
				    {   give (c_slot, envelope);
					xg::SIZE { wide, high };
				    };

			       _ => {   give (c_slot, envelope);
					size;
				    };
			    esac;

			fun loop size
			    =
			    loop (
				select [
				    from_mouse'  ==>  (fn mailop = {  do_mouse mailop;  size;  }),
				    from_other'  ==>  (fn mailop =    do_mom (mailop, size)),
				    take' w_slot ==>  (fn msg    = {  give (r_slot, pos (msg, size));   size;   })
				]
			    );

			make_thread "menu_button" .{
			    #
			    loop size;
			};

			wg::realize_fn  widget
			  {
			    window,
			    size,
			    kidplug => xi::replace_other
					   ( xi::replace_mouse  (kidplug,  take'  m_slot),
					     take'  c_slot
					   )
			  };
		    };

		menu_widget
		    =
		    wg::make_widget
		      {
			root_window,

			args =>   fn () = { background => NULL },

			realize   => menu_realize,

			size_preference_thunk_of
			    =>
			    wg::size_preference_thunk_of  widget
		      };

		(menu_widget, mailop);
	    };
    };

end;



##########################################################################
#   The following is support for outline-minor-mode in emacs.		 #
#  ^C @ ^T hides all Text. (Leaves all headings.)			 #
#  ^C @ ^A shows All of file.						 #
#  ^C @ ^Q Quickfolds entire file. (Leaves only top-level headings.)	 #
#  ^C @ ^I shows Immediate children of node.				 #
#  ^C @ ^S Shows all of a node.						 #
#  ^C @ ^D hiDes all of a node.						 #
#  ^HFoutline-mode gives more details.					 #
#  (Or do ^HI and read emacs:outline mode.)				 #
#									 #
# Local variables:							 #
# mode: outline-minor							 #
# outline-regexp: "[{ \t]*\\(fun \\)"			 		 #
# End:									 #
##########################################################################


## COPYRIGHT (c) 1997 AT&T Research.
## Subsequent changes by Jeff Prothero Copyright (c) 2010,
## released under Gnu Public Licence version 3.
