## brick.pkg

# Compiled by:
#     src/lib/x-kit/tut/bricks/bricks.make6

stipulate
    include threadkit;				# threadkit	is from   src/lib/src/lib/thread-kit/src/core-thread-kit/threadkit.pkg
    #
    package xg =  xgeometry;			# xgeometry	is from   src/lib/x-kit/xclient/xgeometry.pkg
    package w  =  widget;			# widget	is from   src/lib/x-kit/widget/basic/widget.pkg
    package bu =  brick_util;			# brick_util	is from   src/lib/x-kit/tut/bricks/brick-util.pkg
    package bv =  brick_view;			# brick_view	is from   src/lib/x-kit/tut/bricks/brickview.pkg
herein

    package brick:  Brick {			# brick_util	is from   src/lib/x-kit/tut/bricks/brick-util.pkg

	Brick
	    =
	    BRICK
	      { bv:     bv::Brick_View,
		p:      bu::Position,
		#
		good:   Ref( Bool ),
		shown:  Ref( Bool ),
		#
		state:  Ref( bu::State )
	      };

	fun set_good    (BRICK { good,  ... } )   =  good  := TRUE;
	fun set_state   (BRICK { state, ... }, s) =  state := s;
	fun set_shown   (BRICK { shown, ... } ) =  shown := TRUE;
	fun view_of     (BRICK { bv,    ... } ) =  bv;
	fun state_of    (BRICK { state, ... } ) =  *state;
	fun is_shown    (BRICK { shown, ... } ) =  *shown;
	fun is_good     (BRICK { good,  ... } ) =  *good;
	fun position_of (BRICK { p,     ... } ) =  p;
	fun as_widget   (BRICK { bv,    ... } ) =  bv::as_widget  bv;

	fun is_bad b
	    =
	    case (state_of b)
		#
		bu::NO_BRICK_STATE =>  TRUE;
		bu::BAD_STATE _    =>  TRUE;
		_                  =>  FALSE;
	    esac;

	fun enumerate_neighbors no (brick, range, brick_at)
	    =
	    {   p = position_of brick;

		no (brick_at (bu::west_of      p));
		no (brick_at (bu::northwest_of p));
		no (brick_at (bu::northeast_of p));
		no (brick_at (bu::east_of      p));
		no (brick_at (bu::southeast_of p));
		no (brick_at (bu::southwest_of p));

		if (range == bu::LONG)
		    #
		    no (brick_at (bu::west_of      (bu::west_of      p)));
		    no (brick_at (bu::west_of      (bu::northwest_of p)));
		    no (brick_at (bu::northwest_of (bu::northwest_of p)));
		    no (brick_at (bu::northwest_of (bu::northeast_of p)));
		    no (brick_at (bu::northeast_of (bu::northeast_of p)));
		    no (brick_at (bu::northeast_of (bu::east_of      p)));
		    no (brick_at (bu::east_of      (bu::east_of      p)));
		    no (brick_at (bu::east_of      (bu::southeast_of p)));
		    no (brick_at (bu::southeast_of (bu::southeast_of p)));
		    no (brick_at (bu::southeast_of (bu::southwest_of p)));
		    no (brick_at (bu::southwest_of (bu::southwest_of p)));
		    no (brick_at (bu::southwest_of (bu::west_of      p)));
		fi;
	    };

	fun neighbor_count prior (brick, range, brick_at)
	    =
	    {   count = REF 0;

		fun inc v
		    =
		    count := *count + v;

		enumerate_neighbors
		    (inc o prior)
		    (brick, range, brick_at);

	       *count;
	    };

	neighbor_good_count
	    =
	    neighbor_count (fn b =  is_good b ?? 1 :: 0);

	neighbor_bad_count
	    =
	    neighbor_count (fn b =  is_bad b ?? 1 :: 0);

	neighbor_ok_count
	    = 
	    neighbor_count
		(fn b =  case (state_of b)
			     #	
			     bu::OK_STATE => 1;
			     _            => 0;
			 esac
		);

	fun end_show (b, brick_at)
	    =
	    if (not (is_shown b or is_good b))

		count = neighbor_good_count (b, bu::SHORT, brick_at);

		set_state (b, bu::BAD_STATE count);
		bv::end_view (view_of b) (int::to_string count);
		set_shown b;
		#  original decrements bad brick count 
	    fi;

	fun show (b, brick_at)
	    =
	    {   count = neighbor_good_count (b, bu::SHORT, brick_at);

		set_state (b, bu::BAD_STATE count);
		set_shown b;
		bv::show_view (view_of b) (int::to_string count);
	    };


	fun show_and_flood (b, brick_at)
	    =
	    show_af (b, 0)
	    where
		fun show_af (b, count)
		    =
		    if (is_shown b)
			#
			count;
		    else
			p = position_of b;

			count' = if (is_good b)
				     count;
				 else
				     show (b, brick_at);
				     count+1;
				 fi;

			case (state_of b)
			    #
			    bu::BAD_STATE  0
				=>
				show_af (brick_at (bu::southwest_of p),
				show_af (brick_at (bu::southeast_of p),
				show_af (brick_at (bu::east_of      p),
				show_af (brick_at (bu::northeast_of p),
				show_af (brick_at (bu::northwest_of p),
				show_af (brick_at (bu::west_of      p), count'))))));

			    _   => count';
			esac;
		    fi;
	    end;

	fun highlight_on  (BRICK { bv, ... } ) =  bv::highlight_on  bv;
	fun highlight_off (BRICK { bv, ... } ) =  bv::highlight_off bv;

	fun toggle_marking b
	    =
	    case (state_of b)
		#
		bu::UNKNOWN_STATE
		    =>
		    {   bv::mark_view (view_of b);
			set_state (b, bu::OK_STATE);
		    };

		bu::OK_STATE
		    =>
		    {   bv::norm_view (view_of b);
			set_state (b, bu::UNKNOWN_STATE);
		    };

		_   => ();
	    esac;


	fun set_text (BRICK { bv, ... }, txt)
	    =
	    bv::set_text bv txt;


	fun new_brick  root_window  (arg as (pt, _, _))
	    =
	    BRICK
	      { 
		bv    =>  bv::make_brick_view  root_window  arg,
		p     =>  pt,
		#
		good  =>  REF FALSE,
		shown =>  REF FALSE,
		state =>  REF bu::UNKNOWN_STATE
	      };


	fun new_no_brick  root_window  palette
	    =
	    BRICK
	      { 
		bv    =>  bv::make_brick_view  root_window  (xg::point_00, make_mailslot(), palette),
		p     =>  xg::point_00,
		good  =>  REF FALSE,
		shown =>  REF TRUE,
		state =>  REF bu::NO_BRICK_STATE
	      };


	fun reset (BRICK { bv, state, good, shown, ... } )
	    =
	    {   state :=  bu::UNKNOWN_STATE;

		shown :=  FALSE;
		good  :=  FALSE;

		bv::norm_view  bv;
	    };

    };							# package brick

end;


##########################################################################
#   The following is support for outline-minor-mode in emacs.		 #
#  ^C @ ^T hides all Text. (Leaves all headings.)			 #
#  ^C @ ^A shows All of file.						 #
#  ^C @ ^Q Quickfolds entire file. (Leaves only top-level headings.)	 #
#  ^C @ ^I shows Immediate children of node.				 #
#  ^C @ ^S Shows all of a node.						 #
#  ^C @ ^D hiDes all of a node.						 #
#  ^HFoutline-mode gives more details.					 #
#  (Or do ^HI and read emacs:outline mode.)				 #
#									 #
# Local variables:							 #
# mode: outline-minor							 #
# outline-regexp: "[{ \t]*\\(fun \\)"			 		 #
# End:									 #
##########################################################################


## COPYRIGHT (c) 1996 AT&T Research.
## Subsequent changes by Jeff Prothero Copyright (c) 2010,
## released under Gnu Public Licence version 3.
