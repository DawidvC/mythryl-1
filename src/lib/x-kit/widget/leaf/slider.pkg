## slider.pkg
#
# Analog slider.

# Compiled by:
#     src/lib/x-kit/widget/xkit-widget.make6




###                "How does a project get to be a year late?
###                 One day at a time."
###
###                           -- Frederick Brooks, Jr.,
###                              The Mythical Man Month


stipulate
    include threadkit;				# threadkit		is from   src/lib/src/lib/thread-kit/src/core-thread-kit/threadkit.pkg
    #
    package xc =  xcore;			# xcore			is from   src/lib/x-kit/xclient/xcore.pkg
    package xg =  xgeometry;			# xgeometry		is from   src/lib/x-kit/xclient/xgeometry.pkg
    package xi =  xinput;			# xinput		is from   src/lib/x-kit/xclient/xinput.pkg
    #
    package w  =  widget;			# widget		is from   src/lib/x-kit/widget/basic/widget.pkg
    package wa =  widget_attribute;		# widget_attribute	is from   src/lib/x-kit/widget/lib/widget-attribute.pkg
    #
    package sa =  slider_appearance;		# slider_appearance	is from   src/lib/x-kit/widget/leaf/slider-appearance.pkg
herein

    package slider: (weak)  Slider {		# Slider		is from   src/lib/x-kit/widget/leaf/slider.pkg


	fun error (f, msg)
	    =
	    lib_base::failure { module=>"Slider", func=>f, msg };

	Range = { from:  Int,
		  to:    Int
		};


	Mouse_Message
	  #	
	  = GRAB    xg::Point
	  | MOVE    xg::Point
	  | UNGRAB  xg::Point
	  | MSE_IN  Bool
	  ;

	Slider_Rep = { curx:  Int,
		       curv:  Int
		     };

		#  mouse reader 

	Reply_Mail =  OKAY | ERROR;

	Plea_Mail
	  = SET_VALUE   (Int, Oneshot_Maildrop( Reply_Mail ))
	  | GET_VALUE         Oneshot_Maildrop( Int )
	  | GET_RANGE         Oneshot_Maildrop( Range )
	  | GET_ACTIVE        Oneshot_Maildrop( Bool )
	  | SET_ACTIVE        Bool
	  #
	  | DO_REALIZE  { kidplug:  xi::Kidplug,
			  window:   xc::Window,
			  size:     xg::Size
			}
	  ;

	Slider
	    =
	    SLIDER
	      { widget:          w::Widget,
		plea_slot:       Mailslot( Plea_Mail ),
		slider_motion':  Mailop( Int )
	      };

    /*
	fun mseP (mseSlot, m) = let
	      use interact
	      fun downLoop () =
		    case msgBodyOf (do_mailop m) of 
		      MOUSE_LAST_UP { pt, ... } => give (mseSlot, UNGRAB pt)
		    | MOUSE_MOTION { pt, ... } => 
			(give (mseSlot, Move pt);downLoop ())
		    | MOUSE_LEAVE _ => (give (mseSlot, MSE_IN FALSE);downLoop ())
		    | MOUSE_ENTER _ => (give (mseSlot, MSE_IN TRUE);downLoop ())
		    | _ => downLoop () 

	      fun loop () = (
		    case msgBodyOf (do_mailop m) of 
		      MOUSE_FIRST_DOWN { pt, ... } => (
			give (mseSlot, GRAB pt);
			downLoop ()
		      )
		    | MOUSE_ENTER _ => give (mseSlot, MSE_IN TRUE)
		    | MOUSE_LEAVE _ => give (mseSlot, MSE_IN FALSE)
		    | _ => ();
		    loop ())
	      in
		loop ()
	      end
    */
	fun mse_p (mse_slot, mouse_mailop)
	    =
	    loop ()
	    where
		timeout' = timeout_in' (time::from_milliseconds 30);

		filter_count = 5;

		fun motion_loop (pt, 0)
			=>
			{   give (mse_slot, MOVE pt);
			    down_loop();
			};

		    motion_loop (pt, count)
			=>
			select [

			    timeout'
				==>
			       .{   give (mse_slot, MOVE pt);
				    #
				    down_loop ();
				},

			    mouse_mailop
				==>
				(fn mailop
				    =
				    case (xi::envelope_contents  mailop)
					#
					xi::MOUSE_LAST_UP { window_point, ... } =>  give (mse_slot, UNGRAB window_point);
					xi::MOUSE_MOTION  { window_point, ... } =>  motion_loop (window_point, count - 1);

					xi::MOUSE_LEAVE _
					    => 
					    {   give (mse_slot, MSE_IN FALSE);
						#
						motion_loop (pt, count);
					    };

					xi::MOUSE_ENTER _
					    => 
					    {   give (mse_slot, MSE_IN TRUE);
						#
						motion_loop (pt, count);
					    };

					_   =>  motion_loop (pt, count);
				    esac
			       )
			];
		end 

		also
		fun down_loop ()
		    =
		    case (xi::envelope_contents (do_mailop  mouse_mailop))    
			#
			xi::MOUSE_LAST_UP { window_point, ... } =>  give (mse_slot, UNGRAB window_point);
			xi::MOUSE_MOTION  { window_point, ... } =>  motion_loop (window_point, filter_count);

			xi::MOUSE_LEAVE _ => {  give (mse_slot, MSE_IN FALSE);  down_loop ();  };
			xi::MOUSE_ENTER _ => {  give (mse_slot, MSE_IN TRUE );  down_loop ();  };

			_ => down_loop ();
		    esac; 

		fun loop ()
		    =
		    for (;;) {
			#
			case (xi::envelope_contents (do_mailop  mouse_mailop))
			    #
			    xi::MOUSE_FIRST_DOWN { window_point, ... }
				=>
				{   give (mse_slot, GRAB window_point);
				    #
				    down_loop ();
				};

			    xi::MOUSE_ENTER _
				=>
				give (mse_slot, MSE_IN TRUE);

			    xi::MOUSE_LEAVE _
				=>
				give (mse_slot, MSE_IN FALSE);
			    _   => ();
			esac;
		    };

	    end;

	fun buffer (inevt, out_slot)
	    =
	    loop ([],[])
	    where
		fun loop ([],[])
			=>
			loop([],[do_mailop inevt]);

		    loop (q,[])
			=>
			loop([], reverse q);

		    loop (q, q' as (m ! rest))
			=> 
			select [

			    inevt
				==>
				(fn msg = loop (msg ! q, q')),

			    give' (out_slot, m)
				==>
			       .{  loop (q, rest);  }
			];
		end;
	    end;

	fun okay_val ( { from_v, to_v, ... } : sa::Result, v)
	    =
	    from_v <= to_v
		##
		??  (from_v <= v  and  v <= to_v)
		::  (from_v >= v  and  v >= to_v);

	fun realize ( { kidplug, window, size }, result, active, v, plea_slot, val_slot)
	    =
	    config (state, size)
	    where
		mse_slot   = make_mailslot ();
		plea_slot' = make_mailslot ();

		plea'   = take'  plea_slot';
		mouse'  = take'  mse_slot;

		my  xi::KIDPLUG { from_mouse', from_other', ... }
		    = 
		    xi::ignore_keyboard  kidplug;

		state = (v, active, FALSE, FALSE);

		fun config (state, size)
		    =
		    {   drawf     =  sa::drawf (window, size, result);
			pt_to_val =  sa::pt_to_val     (size, result);

			fun do_mom (xi::ETC_REDRAW _, me)
				=>
				{   drawf (me, TRUE);
				    me;
				};

			    do_mom (xi::ETC_RESIZE (xg::BOX { wide, high, ... } ), me)
				=>
				config (me, xg::SIZE { wide, high } );

			    do_mom (_, me)
				=>
				me;
			end;

			fun do_plea (SET_VALUE (v', reply_1shot), state as (v, a, r, d))
				=>
				if (okay_val (result, v'))

				    set (reply_1shot, OKAY);

				    if (v == v')
					NULL;
				    else
					give (val_slot, v');
					THE (v', a, r, d);
				    fi;
				else 
				    set (reply_1shot, ERROR);
				    NULL;
				fi;

			    do_plea (GET_VALUE reply_1shot, state)
				=>
				{   set (reply_1shot, #1 state);
				    NULL;
				};

			    do_plea (GET_RANGE reply_1shot, _)
				=>
				{   set (reply_1shot, { from=> result.from_v, to=> result.to_v } );
				    NULL;
				};

			    do_plea (GET_ACTIVE reply_1shot, state)
				=>
				{   set (reply_1shot, #2 state);
				    NULL;
				};

			    do_plea (SET_ACTIVE b', (v, b, r, d))
				=> 
				if (b == b')  NULL;
				else          THE (v, b', r, d);
				fi;

			    do_plea (_, _)
				=>
				NULL;
			end;

			fun do_mouse (GRAB pt, (v, _, r, _))
				=>
				{   v' = (pt_to_val pt)
					 except _ = v;

				    state = (v', TRUE, r, TRUE);

				    drawf (state, FALSE); 

				    if (v != v')
					#
					give (val_slot, v');
				    fi;

				    state; 
				};

			    do_mouse (MOVE pt, (v, _, r, _))
				=>
				{   v' = (pt_to_val pt)
					 except _ = v;

				    state = (v', TRUE, r, TRUE);

				    if (v != v')
					drawf (state, FALSE);
					give (val_slot, v');
				    fi;

				    state; 
				};

			    do_mouse (UNGRAB pt, (v, _, r, _))
				=>
				{   v' = (pt_to_val pt)
				    except _ = v;

				    state = (v', TRUE, r, FALSE);

				    drawf (state, FALSE);

				    if (v != v')
					#
					give (val_slot, v');
				    fi;

				    state; 
			       };

			    do_mouse (MSE_IN r', me as (v, _, r, d))
				=>
				if (r' == r)
				    me;
				else
				    state' = (v, TRUE, r', d);
				    drawf (state', FALSE); state'; 
				fi;
			end;

			fun active_p (me as (v, a, r, d))
			     =
			    select [

				mouse' ==>
				    (fn m =  active_p (do_mouse (m, me))),

				plea'
				    ==>
				    (fn mailop
					=
					case (do_plea (mailop, me))   

					    NULL    => active_p me;

					    THE me' => if (#2 me')
							   drawf (me', FALSE); 
							   active_p me';
						       else
							   drawf (me', TRUE); 
							   inactive_p me';
						       fi;
					esac),


				from_other' ==>
				    (fn mailop =  active_p (do_mom (xi::envelope_contents mailop, me)))
			    ]

			also
			fun inactive_p (me as (v, a, r, d))
			    =
			    select [

				mouse' ==>
				    fn (MSE_IN r') => inactive_p (v, a, r', d);
				       _           => inactive_p me;
				    end,

				plea' ==>
				    (fn mailop
					=
					case (do_plea (mailop, me))   

					    NULL => inactive_p me;

					    THE me' => if (#2 me')
							   drawf (me', TRUE); 
							   active_p me';
						       else
							   drawf (me', FALSE); 
							   inactive_p me';
						       fi;
					esac),


				from_other' ==>
				    (fn mailop
					=
					inactive_p (do_mom (xi::envelope_contents mailop, me)))
			    ];

			if (#2 state)    active_p state;
			else           inactive_p state;
			fi;
		    };				# fun config

		make_thread  "slider plea"  .{
		    #
		    buffer  (take'  plea_slot,  plea_slot');
		};

		make_thread   "slider from_mouse"  .{
		    #
		    mse_p (mse_slot, from_mouse');
		};

	    end;			# fun realize

	fun init (result, is_active, v, plea_slot, val_slot)
	    =
	    loop (is_active, v)
	    where
		fun do_plea (SET_VALUE (v, reply_1shot), state as (active, _))
			=>
			if (okay_val (result, v))
			    #
			    set (reply_1shot, OKAY);
			    (active, v);
			else
			    set (reply_1shot, ERROR);
			    state;
			fi;

		    do_plea (GET_VALUE reply_1shot, state as (_, v))
			=>
			{   set (reply_1shot, v);
			    state;
			};

		    do_plea (GET_RANGE reply_1shot, state)
			=>
			{	set (reply_1shot, { from=> result.from_v, to=> result.to_v } );
			    state;
			};

		    do_plea (GET_ACTIVE reply_1shot, state)
			=>
			{   set (reply_1shot, #1 state);
			    state;
			};

		    do_plea (SET_ACTIVE b, (_, v))
			=>
			(b, v);

		    do_plea (DO_REALIZE arg, (active, v))
			=> 
			{   realize (arg, result, active, v, plea_slot, val_slot);
			    (active, v);
			};
		end;

	       fun loop state
		   =
		   loop (do_plea (take plea_slot, state));

	    end;

	fun get_current (NULL, result)
		=>
		result.from_v;

	    get_current (THE v, result)
		=>
		if (okay_val (result, v))   v;
		else                        error ("slider", "current value out of range");
		fi;
	end;

	attributes
	    =
	    [ (wa::is_active,   wa::BOOL,    wa::BOOL_VAL TRUE),
	      (wa::current,     wa::INT,     wa::NO_VAL)
	    ];

	fun make_slider (root_window, view, args)
	    =
	    {   attributes = w::find_attribute (w::attributes (view, attributes @ sa::attributes, args));

		result = sa::get_resources (root_window, attributes);

		is_active = wa::get_bool (attributes wa::is_active);

		v = get_current (wa::get_int_opt (attributes wa::current), result);

		val_slot     =  make_mailslot ();
		plea_slot =  make_mailslot ();

		make_thread  "slider"  .{
		    #
		    init (result, is_active, v, plea_slot, val_slot);
		};

		SLIDER
		  {
		    plea_slot,

		    slider_motion'
			=>
			take' val_slot,

		    widget => w::make_widget { root_window,
					       args      =>  fn ()  =  { background => THE result.bg },
					       realize   =>  fn arg =  give (plea_slot, DO_REALIZE arg),
					       size_preference_thunk_of =>  sa::size_preference_thunk_of result
					     }
		  };
	    };

	fun as_widget         (SLIDER { widget,          ... } ) =  widget;
	fun slider_motion'_of (SLIDER { slider_motion',  ... } ) =  slider_motion';

	fun set_slider_value (SLIDER { plea_slot, ... } ) v
	    =
	    {   reply_1shot = make_oneshot_maildrop ();

		give (plea_slot, SET_VALUE (v, reply_1shot));

		case (get reply_1shot)   
		    #
		    OKAY => ();
		    ERROR => error("setValue", "improper value");
		esac;
	    };


	stipulate

	    fun get_msg msg (SLIDER { plea_slot, ... } )
		=
		{   reply_1shot = make_oneshot_maildrop ();

		    give (plea_slot, msg reply_1shot);

		    get reply_1shot;
		};
	herein

	    get_slider_range       = get_msg GET_RANGE;
	    get_slider_value       = get_msg GET_VALUE;
	    get_slider_active_flag = get_msg GET_ACTIVE;

	end;


	fun set_slider_active_flag (SLIDER { plea_slot, ... }, b)
	    =
	    give (plea_slot, SET_ACTIVE b);

    };			# package slider 

end;


##########################################################################
#   The following is support for outline-minor-mode in emacs.		 #
#  ^C @ ^T hides all Text. (Leaves all headings.)			 #
#  ^C @ ^A shows All of file.						 #
#  ^C @ ^Q Quickfolds entire file. (Leaves only top-level headings.)	 #
#  ^C @ ^I shows Immediate children of node.				 #
#  ^C @ ^S Shows all of a node.						 #
#  ^C @ ^D hiDes all of a node.						 #
#  ^HFoutline-mode gives more details.					 #
#  (Or do ^HI and read emacs:outline mode.)				 #
#									 #
# Local variables:							 #
# mode: outline-minor							 #
# outline-regexp: "[{ \t]*\\(fun \\)"			 		 #
# End:									 #
##########################################################################


## COPYRIGHT (c) 1994 by AT&T Bell Laboratories  See COPYRIGHT file for details.
## Subsequent changes by Jeff Prothero Copyright (c) 2010,
## released under Gnu Public Licence version 3.
