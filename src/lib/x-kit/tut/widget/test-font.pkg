## test-font.pkg

# Compiled by:
#     src/lib/x-kit/tut/widget/widgets.make6


stipulate
    include threadkit;					# threadkit	is from   src/lib/src/lib/thread-kit/src/core-thread-kit/threadkit.pkg
    #
    package rx =  run_xkit;				# run_xkit	is from   src/lib/x-kit/widget/lib/run-xkit.pkg
    package xf =  xfont;				# xfont		is from   src/lib/x-kit/xclient/xfont.pkg
    package wg =  widget;				# widget	is from   src/lib/x-kit/widget/basic/widget.pkg
herein

    package test_font:  api {
			    do_it':  (List(String), String) -> Void;
			    do_it:    Void -> Void;
			    main:    (List(String), X) -> Void;
			}
    {

	fun print_info (msg, xf::CHAR_INFO info)
	    =
	    {   threadkit_file::print msg;

		threadkit_file::print
		    (sprintf ": lb %d rb %d wd %d\n"
			info.left_bearing
			info.right_bearing
			info.char_width
		    );
	    };

	fun font_info font
	    =
	    {   my { min_bounds, max_bounds, min_char, max_char }
		    =
		    xf::font_info_of font;

		char_info
		    =
		    xf::char_info_of  font;

		fun loop c
		    = 
		    if (c != max_char)

			info = char_info c;

			print_info (int::to_string c, info);
			loop (c+1);
		    fi
		    except
			xf::NO_CHAR_INFO
			    =
			    loop (c+1);

		threadkit_file::print
		    (sprintf "min_char = %d max_char = %d\n"
		       min_char
		       max_char
		    ); 

		print_info ("min_bounds", min_bounds);
		print_info ("max_bounds", max_bounds);

		loop min_char;
	    };

	fname = "-Adobe-Helvetica-Bold-R-Normal--*-120-*";

	fun goodbye root
	    =
	    {   fun quit ()
		    =
		    {   wg::delete_root_window root;
			shut_down_thread_scheduler 0;
		    };

		font = wg::open_font root fname;

		font_info font;
		quit ();
	    };

	fun do_it' (debug_flags, server)
	    =
	    {   xtracing::init  debug_flags;

		rx::run_xkit'
		    #
		    goodbye
		    #
		    { display => THE server,
		      time_quantum_in_milliseconds => NULL
		    };
	    };

	fun do_it ()
	    =
	    rx::run_xkit  goodbye;

	fun main (program ! server ! _, _) =>  do_it'([], server);
	    main _                         =>  do_it ();
	end;

    };				# package test_font
end;

## COPYRIGHT (c) 1991, 1995 by AT&T Bell Laboratories.  See COPYRIGHT file for details.
## Subsequent changes by Jeff Prothero Copyright (c) 2010,
## released under Gnu Public Licence version 3.
