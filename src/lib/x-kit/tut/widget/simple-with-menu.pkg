## simple-with-menu.pkg

# Compiled by:
#     src/lib/x-kit/tut/widget/widgets.make6


# Test the popup-menu package.

stipulate
    include threadkit;				# threadkit		is from   src/lib/src/lib/thread-kit/src/core-thread-kit/threadkit.pkg
    #
    package xi  =  xinput;			# xinput		is from   src/lib/x-kit/xclient/xinput.pkg
    #
    package rx  =  run_xkit;			# run_xkit		is from   src/lib/x-kit/widget/lib/run-xkit.pkg
    package top =  topwindow;			# topwindow		is from   src/lib/x-kit/widget/basic/topwindow.pkg
    package w   =  widget;			# widget		is from   src/lib/x-kit/widget/basic/widget.pkg
    package wa  =  widget_attribute;		# widget_attribute	is from   src/lib/x-kit/widget/lib/widget-attribute.pkg
    package wy  =  widget_style;		# widget_style		is from   src/lib/x-kit/widget/lib/widget-style.pkg
    #
    package pu  =  popup_menu;			# popup_menu		is from   src/lib/x-kit/widget/menu/popup-menu.pkg
    package low =  line_of_widgets;		# line_of_widgets	is from   src/lib/x-kit/widget/layout/line-of-widgets.pkg
    package pb  =  pushbuttons;			# pushbuttons		is from   src/lib/x-kit/widget/leaf/pushbuttons.pkg
herein

    package simple_with_menu:   api {
				    do_it': (List(String), String) -> Void;
				    do_it:   Void -> Void;
				    main:   (List( String ), X) -> Void;
				}
    {

	resources = [
	    "*background: forestgreen"
	  ];

	menu1 =
	    pu::POPUP_MENU
	      [
		pu::POPUP_MENU_ITEM("item-1", 1),
		pu::POPUP_MENU_ITEM("item-2", 2),
		pu::POPUP_MENU_ITEM("item-3", 3),

		pu::POPUP_SUBMENU("submenu1",
		    pu::POPUP_MENU
		      [ pu::POPUP_MENU_ITEM("item-4", 4),
			pu::POPUP_MENU_ITEM("item-5", 5),
			pu::POPUP_MENU_ITEM("item-6", 6)		    
		      ]
		),

		pu::POPUP_MENU_ITEM("item-7", 7)
	      ];

	fun goodbye root
	    = 
	    {   make_thread "popup with menu" monitor;

		top::start  topwindow;

		loop ();
	    }
	    where
		fun quit ()
		    =
		    {   w::delete_root_window root;
			shut_down_thread_scheduler  winix::process::success;
		    };

		style = w::style_from_strings (root, resources);

		name = wy::make_view
			 { name    =>   wy::style_name [],
			   aliases => [ wy::style_name [] ]
			 };

		view = (name, style);

		args = [ (wa::label, wa::STRING_VAL "Goodbye, Cruel World!") ];

		bttn = pb::make_text_pushbutton_with_click_callback' (root, view, args) quit;

		layout
		    =
		    low::line_of_widgets
			(root, view,[])
			(low::VT_CENTER
			  [
			    low::SPACER { min_size=>0,  ideal_size=>30, max_size=>NULL },
			    low::WIDGET (pb::as_widget bttn),
			    low::SPACER { min_size=>0,  ideal_size=>30, max_size=>NULL }
			  ]
			);

		my (widget, mailop)
		    =
		    pu::attach_menu_to_widget
		      ( low::as_widget layout, 
			[ xi::MOUSEBUTTON 3],
			menu1
		      );

		fun monitor ()
		    =
		    for (;;) {

			n = do_mailop  mailop;

			file::write (file::stdout, "menu choice " + int::to_string n + "\n");
		    };

		topwindow = top::topwindow  (root, view,[])  widget;

		fun loop ()
		    =
		    case (file::read_line file::stdin)
			#
			THE string
			    =>
			    string ==  "quit\n"  ??  quit ()
						 ::  loop ();
			NULL
			    =>
			    quit ();
		    esac;

	    end;				# fun goodbye

	fun do_it' (debug_flags, server)
	    =
	    {   xtracing::init debug_flags;

		rx::run_xkit'
		    #
		    goodbye
		    #
		    { display => THE server,
		      time_quantum_in_milliseconds => NULL
		    };
	    };

	fun do_it ()
	    =
	    rx::run_xkit  goodbye;


	fun main (program ! server ! _, _) =>  do_it'([], server);
	    main _                         =>  do_it ();
	end;

    };						# package simple_with_menu 
end;

## COPYRIGHT (c) 1991 by AT&T Bell Laboratories.  See COPYRIGHT file for details.
## Subsequent changes by Jeff Prothero Copyright (c) 2010,
## released under Gnu Public Licence version 3.
