
# This module can be used to generate reflection code
# from ML enum definitions

generic package lib7_reflect_gen_g (ml_grinder:  Ml_Grinder) : Lib7_Reflect_Gen {

   package g = ml_grinder
   use G
   package raw = raw
   package a = raw
   package u = raw_util
   package t = raw_trans

   argpat    = a::IDpat "arg"
   argexp    = u::ID "arg"
   tv_prefix = "f_" #  higher order function arguments 

   fun reflect { prefix, makeArg, makeCons, makeConsPat } 
               { idty, appty, funty, tuplety, recordty } 
               datatypeDecls
       =
       {   fun fnName id
               =
               prefix + id;

	   #  Generate pretty printer for a enum 

	   fun gen_db (a::DATATYPEbind { id, cbs, tyvars, ... } )
                   =
		   [a::FUNbind (fnName id, map (gen_clause tyvars) cbs)];

	     | gen_db _ = [];

	  also arity_raise [] = []
	     | arity_raise tvs
                   = 
		   [a::TUPLEpat (map (fn a::VARtv x => a::IDpat (tv_prefix + x)
				  | _ => bug("lib7_reflect_gen_g", "arity_raise")
				) tvs)]

	   #  Generate pretty printer for a clause 
	  also gen_clause tyvars (cons as a::CONSbind { id=name, ty, ... } )
               = 
	       {   args =   arity_raise tyvars;
		   pats =   makeArg (makeConsPat (name, null_or::map (fn _ => argpat) ty));
		   body = makeCons (name,
				 case ty of
				   NULL => NULL
				 | THE ty => THE (a::APPexp (gen_ty ty, argexp)));
	           a::CLAUSE (args@pats, NULL, body);
	       }

	   #  Generate pretty printer for a type synonym 
	  also gen_tb (a::TYPEbind (id, tyvars, ty))
               = 
	       a::FUNbind (fnName id,
			 [a::CLAUSE (arity_raise tyvars@makeArg argpat, NULL,
				   a::APPexp (gen_ty ty, argexp))])

	  also gen_ty (a::IDty x) = idty x
	     | gen_ty (a::APPty (f, tys)) = appty (f, map gen_ty tys)
	     | gen_ty (a::FUNty (x, y)) = funty (gen_ty x, gen_ty y)
	     | gen_ty (a::TUPLEty tys) = tuplety (map gen_ty tys)
	     | gen_ty (a::RECORDty ltys) = recordty (map gen_lty ltys)
	     | gen_ty (a::TYVARty (a::VARtv tv)) = u::ID (tv_prefix + tv)
	     | gen_ty _ = bug("lib7_reflect_gen_g", "gen_ty")

	  also gen_lty (x, ty)
               =
               (x, gen_ty ty);

	   prog = decl::fold 
		      [Fold::D (fn (a::DATATYPEdecl (dbs, tbs), decls) => 
				 a::FUNdecl (map gen_tb tbs@
					   list::cat (map gen_db dbs)) . decls
			      | (_, decls) => decls)
		      ] [] datatypeDecls;

           a::SEQdecl (reverse prog);
       }
}
