## chunk-desc.pkg

# Compiled by:
#     src/lib/compiler/core.make6


# The encoding of chunk description headers.
#
# WARNING: this file must agree with the run-time system values and those
# used in boot/core.pkg and boot/unsafe/unsafe-chunk.pkg.


package chunk_desc: Chunk_Desc {		# Chunk_Desc	is from   src/lib/compiler/codegen/main/chunk-desc.api

    Tag = Unt;

    package w= unt;		# unt		is from   src/lib/std/unt.pkg
    package lw= large_unt;	# large_unt	is from   src/lib/std/large-unt.pkg

    #  taken from runtime/tags.h 
    tag_width = 7;		#  5 minor tag bits plus 2 major tag bits 

    #  one greater than the maximum length field value (sign should be 0) 

    max_length   = w::to_int (w::(<<) (0u1, w::(-) (0u31, w::from_int tag_width)));
    pow_tag_width = w::to_int (w::(<<) (0u1, w::from_int tag_width));

    # Tag values:
    #
    stipulate
        fun make_tag t = w::bitwise_or (w::(<<) (t, 0u2), 0u2);
    herein
	tag_record	= make_tag 0u0;
	tag_vec_hdr	= make_tag 0u1;
	tag_vec_data	= tag_record;
	tag_arr_hdr	= make_tag 0u2;
	tag_arr_data	= make_tag 0u3;
	tag_ref	= tag_arr_data;
	tag_raw32	= make_tag 0u4;
	tag_raw64	= make_tag 0u5;
	tag_special	= make_tag 0u6;
    end;



    # Build a descriptor from a tag and length 

    fun make_desc (len, t)
        = 
	lw::bitwise_or (lw::(<<) (lw::from_int len, w::from_int tag_width), w::to_large_unt t);

    # rw_vector/Vector header codes 

    seq_poly		= 0;
    seq_word8	= 1;
    seq_word16	= 2;
    seq_word31	= 3;
    seq_word32	= 4;
    seq_real32	= 5;
    seq_real64	= 6;

    # Fixed descriptors:
    #
    desc_pair    = make_desc (2, tag_record);
    desc_ref     = make_desc (1, tag_ref);
    desc_real64  = make_desc (2, tag_raw64);
    desc_polyvec = make_desc (seq_poly, tag_vec_hdr);
    desc_polyarr = make_desc (seq_poly, tag_arr_hdr);
    desc_special = make_desc (0, tag_special);

    # Special descriptors:
    #
    special_unevaled_susp = 0;
    special_evaled_susp	  = 1;
    special_weak	  = 2;
    special_nulled_weak	  = 3;
};




## COPYRIGHT (c) 1998 Bell Labs, Lucent Technologies.
## Subsequent changes by Jeff Prothero Copyright (c) 2010,
## released under Gnu Public Licence version 3.
