## finalize-g.pkg
## AUTHOR:  John Reppy
##	    AT&T Bell Laboratories
##	    Murray Hill, NJ 07974
##	    jhr@research.att.com

# Compiled by:
#     src/lib/src/lib7.make6


generic package finalize_g (chunk:  Finalized_Chunk)
:
Finalize
{
    package chunk =  chunk;
    package w     =  weak_reference;


    chunk_list
        =
        REF ([] :  List( (w::Weak( chunk::Chunk ), chunk::Chunk_Info)) );


    fun register_chunk (chunk, info)
        =
        chunk_list
            :=
            (w::weak chunk, info) ! *chunk_list;


    fun prune ([], live, dead)
            =>
            (live, dead);

        prune ((x as (wp, info)) ! r, live, dead)
            =>
            case (w::strong wp)
	      
                   THE _  =>  prune (r, x ! live,        dead);
	           NULL   =>  prune (r,     live, info ! dead);
            esac;
    end;

    fun get_dead ()
        =
        {   my (live, dead)
                =
                prune (*chunk_list, [], []);

	    chunk_list := live;
	    dead;
	};

    fun finalize ()
        =
        {   fun reap ([], live)
                    =>
                    live;

	        reap ((x as (wp, info)) ! r, live)
                    =>
                    case (w::strong wp)
                      
		         THE _
                             =>
                             reap (r, x ! live);

		         NULL
                             =>
                             {   chunk::finalize info;
                                 reap (r, live);
                             };
                    esac;
	    end;

	    chunk_list
                :=
                reap (*chunk_list, []);
	};

};			# finalize_g


## COPYRIGHT (c) 1991 by AT&T Bell Laboratories.  See COPYRIGHT file for details.
## Subsequent changes by Jeff Prothero Copyright (c) 2010,
## released under Gnu Public Licence version 3.
