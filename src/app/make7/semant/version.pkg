## Version numbering for Make7 libraries.
## (C) 2000 Lucent Technologies, Bell Laboratories
## Author: Matthias Blume (blume@kurims.kyoto-u.ac.jp)

# Compiled by:
#     src/app/make7/Make7.make6



api Version {

    Version;

    from_string:  String  -> Null_Or( Version );
    to_string:    Version -> String;
    compare:     (Version, Version) -> Order;

    next_major:   Version -> Version;

    zero: Version;
};

package version: Version {

    Version
        =
        { major: Int,
          minor: List( Int )
        };

    fun from_string s
        =
	{   fun convert (_, NULL)
                    =>
                    NULL;

	        convert (s, THE l)
		    =>
		    case (int::from_string s)
                      
			 THE i =>  THE (i ! l);
			 NULL  =>  NULL;
                    esac;
            end;
	
	    case (fold_right
                     convert
                     (THE [])
                     (string::fields   (fn c =  c == '.')   s))

	      
                 THE (maj ! min)
		     =>
		     THE {   major => maj,
			      minor => min
			  };

	         _   =>
                     NULL;
            esac;
	};

    fun to_string { major, minor }
        =
	cat ( int::to_string major
               !
	       fold_right
                   (fn (i, l) =  "." ! int::to_string i ! l)
		   []
                   minor
             );

    fun compare ( v1: Version,
                  v2: Version
                )
        =
	lcmp ( v1.major ! v1.minor,
               v2.major ! v2.minor
             )
        where
            fun lcmp ([], []) =>  EQUAL;
		lcmp ([], _ ) =>  LESS;
		lcmp (_, [])  =>  GREATER;

		lcmp (h ! t, h' ! t')
		    =>
		    case (int::compare (h, h'))
		      
		         EQUAL   =>  lcmp (t, t');
			 unequal =>  unequal;
                    esac;
            end;
	end;

    fun next_major (v: Version)
        =
        {   major =>  v.major + 1,
            minor =>  []
        };

    zero
        =
        { major =>  0,
          minor =>  []
        };
};


##########################################################################
#   The following is support for outline-minor-mode in emacs.		 #
#  ^C @ ^T hides all Text. (Leaves all headings.)			 #
#  ^C @ ^A shows All of file.						 #
#  ^C @ ^Q Quickfolds entire file. (Leaves only top-level headings.)	 #
#  ^C @ ^I shows Immediate children of node.				 #
#  ^C @ ^S Shows all of a node.						 #
#  ^C @ ^D hiDes all of a node.						 #
#  ^HFoutline-mode gives more details.					 #
#  (Or do ^HI and read emacs:outline mode.)				 #
#									 #
# Local variables:							 #
# mode: outline-minor							 #
# outline-regexp: "[{ \t]*\\(fun \\)"			 		 #
# End:									 #
##########################################################################
