api DERIVATIVE_TACTICS =
api

    exception TacticFailure

    type goal
    type derivative
    type tactic
    type validation

    my sinTac:  tactic
    my cosTac:  tactic
    my addTac:  tactic
    my productTac:  tactic
    my chainTac:  tactic
    my negTac:  tactic
    my eTac:  tactic
    my expTac:  tactic
    my singleVarTac:  tactic
    my constantTac:  tactic

end

package DerivativeTactics = 
pkg

    use Expression DerivativeRules

    exception TacticFailure

    type goal = expression
    type derivative = derivative
    type validation = (List( derivative ) -> derivative)
    type tactic = (goal -> List( goal ) * validation)

    my sinTac:  tactic =
	fn (expr as (Sin (Term (Var x)))) => ([], fn l => (sinRule expr))
	 | _  => raise TacticFailure

    my cosTac:  tactic =
	fn (expr as (Cos (Term (Var x)))) => ([], fn l => (cosRule expr))
	 | _ => raise TacticFailure

    my singleVarTac:  tactic =
        fn (expr as (Term (Var x))) => ([], fn l => (singleVarRule expr))
	 | _  => raise TacticFailure

    my constantTac:  tactic =
        fn (expr as (Term (Num n))) => ([], fn l => (constantRule expr))
	 | _ => raise TacticFailure

    my expTac:  tactic =
	fn (expr as (Exp (Term (Var x), Term (Num n)))) => ([], fn l => (exponentRule expr))
	 | _  => raise TacticFailure

    my eTac:  tactic =
	fn (e_of_f as (E f)) => ([f], fn [f'] => eRule (f', e_of_f) | _ => raise TacticFailure)
	 | _  => raise TacticFailure 

    my addTac:  tactic =
	fn (Plus (f, g)) => ([f, g], fn [f', g'] => additionRule (f', g') | _ => raise TacticFailure)
	 | (Minus (f, g)) => ([f, g], fn [f', g'] => subtractionRule (f', g') | _ => raise TacticFailure)
	 | _ => raise TacticFailure
   
    my productTac:  tactic =
	fn (Times (f, g)) => ([f, g], fn [f', g'] => productRule (f', g', f, g) | _ => raise TacticFailure)
	 | _ => raise TacticFailure

    my chainTac:  tactic =
	fn (Sin g) => 
	   ([Sin (Term (Var "x")), g],
	    fn [f_of_x', g'] => chainRule (f_of_x', g', "x") | _ => raise TacticFailure)

	 | (Cos g) => 
	    ([Cos (Term (Var "x")), g],
	     fn [f_of_x', g'] => chainRule (f_of_x', g', "x") | _ => raise TacticFailure)

	 | (Exp (g, Term (Num n))) =>
	    ([Exp (Term (Var "x"), Term (Num n)), g], 
	     fn [f_of_x', g'] => chainRule (f_of_x', g', "x") | _ => raise TacticFailure)

	 | _ => raise TacticFailure

    my negTac:  tactic =
	fn (Neg g) =>
	   ([g], fn [g'] => negRule (g') | _ => raise TacticFailure)

end







