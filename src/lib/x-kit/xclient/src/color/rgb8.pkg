## rgb8.pkg
#
# RGB color representations at 8 bits per color component,
# 24 bits total.
#
# I'm currently hardwiring this format as
#
#     red   = 0xFF0000
#     green = 0x00FF00
#     blue  = 0x0000FF
#
# Technically, X allows any number of bits per
# color value, different numbers of bits per color
# value, arbitrary ordering of color fields etc,
# but my impression is that in practice everyone
# is using the above format these days.
#
# See also:
#     src/lib/x-kit/xclient/src/color/hue-saturation-value.pkg
#     src/lib/x-kit/xclient/src/color/rgb.pkg

# Compiled by:
#     src/lib/x-kit/xclient/xclient-internals.make6






package rgb8: Rgb8 {				# Rgb8		is from   src/lib/x-kit/xclient/src/color/rgb8.api

    Rgb8 = Int;


    fun from_int i
	=
	i;


    fun to_int i
	=
	i;


    fun from_ints (r, g, b)
	=
	{   r = r & 0xFF;
	    g = g & 0xFF;
	    b = b & 0xFF;

	    i = (r << 16)
	      | (g <<  8)
	      | (b      )
	      ;

	    i;
	};

    fun to_ints i
	=
	{   r = (i >> 16); 
	    g = (i >>  8) & 0xFF; 
	    b = (i      ) & 0xFF; 

	    (r, g, b);
	};


    fun from_floats (red, green, blue)
	=
	from_ints
	  ( float_to_int  red,
	    float_to_int  green,
	    float_to_int  blue
	  )
	where
	    fun float_to_int f
		=
		{   f =  (f < 0.0) ?? 0.0 :: f;
		    f =  (f > 1.0) ?? 1.0 :: f;

		    f =   f * 255.99;		# Our ints run 0 -> 255.

		    float::truncate  f;
		};
	end;


    fun to_floats  rgb8
	=
	{   (to_ints  rgb8)
		->
		(red, green, blue);

	    ( int_to_float  red,
	      int_to_float  green,
	      int_to_float  blue
	    );
	}
	where
	    fun int_to_float i
		=
		{   f  = float::from_int  i;

		    f  = f // 255.0;		# Our ints run 0 -> 255.

		    f;
		};
	end;


    fun from_rgb  rgb
	=
	from_floats (rgb::to_floats rgb);


    fun to_rgb  rgb8
	=
	rgb::from_floats (to_floats  rgb8);


    fun same (i, j)
	=
	i == j;


    fun from_name  colorname
	=
	from_ints  (x11_color_name::to_ints  colorname);
};


## COPYRIGHT (c) 1994 by AT&T Bell Laboratories
## Subsequent changes by Jeff Prothero Copyright (c) 2010,
## released under Gnu Public Licence version 3.
