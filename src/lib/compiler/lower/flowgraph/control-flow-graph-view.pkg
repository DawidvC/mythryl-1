## control-flow-graph-view.pkg -- graphical viewing utilities for cfg 



generic package control_flow_graph_view
  (package asm:  Instruction_Emitter 
   package control_flow_graph:  Control_Flow_Graph
		   where I = Asm::I
		     and P = asm::instruction_stream::P
  ) : Control_Flow_Graph_Viewer
{
   package l   = graph_layout
   package control_flow_graph = control_flow_graph
   package g = graph
   package w = Freq
   package s = Asm::S
   
   fun nl () = file::write(*asm_stream::asmOutStream, "\n")

   fun kindName control_flow_graph::START          = "START"
     | kindName control_flow_graph::STOP           = "STOP"
     | kindName control_flow_graph::NORMAL         = "Block"


   fun emitHeader (s::STREAM { comment, annotation, ... } ) 
                   (control_flow_graph::BLOCK { id, kind, freq, annotations, ... } ) = 
       (comment (kindName kind + "[" + int::to_string id +
                    "] (" + w::to_string (*freq) + ")");
        nl();
        apply annotation (*annotations)
       ) 

   fun emitFooter (s::STREAM { comment, ... } ) (control_flow_graph::BLOCK { annotations, ... } )
       = 
       case (control_flow_graph::LIVEOUT.get (*annotations))

            THE s
		=> 
		let regs = string::tokens char::is_space (cells_basis::CellSet::to_string s)
		    K = 7
		    fun f (_,[], s, l)    = s . l
		      | f (0, vs, s, l)    = f (K, vs, "   ", s . l)
		      | f (n,[v], s, l)   = v + s . l
		      | f (n, v . vs, s, l) = f (n - 1, vs, s + " " + v, l)
		    text = reverse (f(K, regs, "",[]))
		in  apply (fn c => (comment c; nl())) text
		end;

            NULL => ();
        esac
        except
            OVERFLOW = print("Bad footer\n");

  fun emitStuff outline annotations (block as control_flow_graph::BLOCK { instructions, labels, ... } )
      =
       let my S as s::STREAM { pseudoOp, define_local_label, emit, ... } = 
               Asm::make_stream annotations
       in  emitHeader S block;
           apply define_local_label (*labels); 
           if outline then () else apply emit (reverse (*instructions));
           emitFooter S block
       end

    emit = emitStuff FALSE 
    emitOutline = emitStuff TRUE []

   fun get_string f x = 
   let buffer = StringOutStream::mkStreamBuf()
       S      = StringOutStream::openStringOut buffer
       asm_stream::with_stream S f x 
   in  StringOutStream::get_string buffer end

   fun show_block an block = 
   let text = get_string (emit an) block
   in  fold_right (fn (x, "") => x | (x, y) => x + " " + y) ""
            (string::tokens (fn ' ' => TRUE | _ => FALSE) text)
   end
   
   fun headerText block = get_string 
        (fn b => emitHeader (Asm::make_stream []) b) block
   fun footerText block = get_string 
        (fn b => emitFooter (Asm::make_stream []) b) block

   fun getStyle a = (case l::STYLE.get (*a) of THE l => l | NULL => [])

   green = l::COLOR "green"
   red   = l::COLOR "red"
   yellow = l::COLOR "yellow"

   show_edge = control_flow_graph::show_edge

   fun edgeStyle (i, j, e as control_flow_graph::EDGE { k, a, ... } ) = 
   let a = l::LABEL (show_edge e) . getStyle a
   in  case k of 
         (control_flow_graph::ENTRY | control_flow_graph::EXIT) => green . a
       | (control_flow_graph::FALLSTHRU | control_flow_graph::BRANCH FALSE) => yellow . a
       | _ => red . a
   end 

   view_outline = LowCodeControl::getFlag "view_outline"
 
   fun annotations (g::GRAPH { graph_info=control_flow_graph::INFO { annotations=a, ... }, ... } ) = a

   fun viewStyle cfg =
   let an     = *(annotations cfg)
       fun node (n, b as control_flow_graph::BLOCK { annotations, ... } ) = 
           if *view_outline then
              l::LABEL (get_string emitOutline b) . getStyle annotations
           else
              l::LABEL (show_block an b) . getStyle annotations
   in  { graph = fn _ => [],
         edge  = edgeStyle,
         node  = node
       } 
   end

   fun viewLayout cfg = l::makeLayout (viewStyle cfg) cfg

   fun subgraphLayout { cfg, subgraph = g::GRAPH subgraph }
   =
   let an     = *(annotations cfg)
       fun node (n, b as control_flow_graph::BLOCK { annotations, ... } ) = 
          if subgraph.has_node n then
             l::LABEL (show_block an b) . getStyle annotations
          else
             l::COLOR "lightableue" . l::LABEL (headerText b) . getStyle annotations
       fun edge (i, j, e) = 
            if subgraph.has_edge (i, j) then edgeStyle (i, j, e)
            else [l::EDGEPATTERN "dotted"]
   in  l::makeLayout { graph = fn _ => [],
                     edge  = edge,
                     node  = node } cfg
   end
}





## Copyright (c) 2001 Bell Laboratories.
## Subsequent changes by Jeff Prothero Copyright (c) 2010,
## released under Gnu Public Licence version 3.
