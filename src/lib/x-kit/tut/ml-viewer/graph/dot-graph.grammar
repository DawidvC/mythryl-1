## dot-graph.grammar
## COPYRIGHT (c) 1994 AT&T Bell Laboratories.

# Parser specification for the "dot" style syntax
# defined by GraphViz.  For docs on it see:
#     http://www.graphviz.org/Documentation.php
# (The version specified here may well be a decade
# or so behind the above specifications.)

exception ERROR;

Vertex
  = NODE (String, Null_Or( String )) 
  | SUBGRAPH (g::Attributed_Graph -> g::Attributed_Graph)
  ;

fun ga_fn g = g::set_attr(g::GRAPH_PART g);		# "ga_fn" may be "graph_attribute_function"
fun na_fn n = g::set_attr(g::NODE_PART n);		# "na_fn" may be "node_attribute_function"
fun ea_fn e = g::set_attr(g::EDGE_PART e);		# "ea_fn" may be "edge_attribute_function"

fun pna_fn g = g::set_attr(g::PROTONODE_PART g);	# "pna_fn" may be "proto_node_attribute_function"
fun pea_fn g = g::set_attr(g::PROTOEDGE_PART g);	# "pea_fn" may be "proto_edge_attribute_function"

fun find_subgraph (g, name)
    =
    case (g::find_subgraph (g, name))
	#
        THE sg => sg;
        NULL   => raise exception ERROR;
    esac;

stipulate
    count = REF 0;
herein
    fun anonymous ()
	=
	("_anonymous_" + (int::to_string *count))
        before
	    count := *count + 1;
end;

stipulate

    fun make_port_fn (NULL,     NULL) =>   fn _ =  ();
        make_port_fn (THE tp,   NULL) =>   fn e =   ea_fn e ("tailport",tp);
        make_port_fn (NULL,   THE hp) =>   fn e =   ea_fn e ("headport",hp);
        make_port_fn (THE tp, THE hp) => { fn e = { ea_fn e ("headport",hp);
                                                    ea_fn e ("tailport",tp);
                                                  };
                                         };
    end;

herein

    fun make_edges (vs, attributes) g
	=
        {
	    fun do_edge portfn (t,h)
		=
		{   e = g::add_edge{ graph=>g,hd=>h,tl=>t,info=>NULL };

		    portfn e;
		    apply (ea_fn e) attributes;
		};

	    fun mk_e (tl ! (rest as hd ! l))
		    =>
		    case (tl, hd)
			#
		        (NODE(t,tport), NODE(h,hport))
			    =>
			    {   do_edge (make_port_fn(tport,hport)) (g::get_node(g,t,NULL), g::get_node(g,h,NULL));
			        mk_e rest;
			    };

		        (NODE(name,port), SUBGRAPH mkg)
			    =>
			    {   edgefn = do_edge (make_port_fn(port,NULL));
			        t      = g::get_node(g,name,NULL);
			        subg   = mkg g;					# "subg" might be "subgraph"

			        g::apply_nodes (fn n = edgefn(t,n)) subg;

		                mk_e((SUBGRAPH(fn _ = subg)) ! l);
			    };

		        (SUBGRAPH mkg, NODE(name,port))
			    =>
			    {   edgefn =  do_edge (make_port_fn(NULL, port));
			        h      =  g::get_node(g,name,NULL);

			        g::apply_nodes (fn n = edgefn(n,h)) (mkg g);

			        mk_e rest;
			    };

		        (SUBGRAPH mkg, SUBGRAPH mkg')
			    =>
			    {   edgefn = do_edge (make_port_fn(NULL, NULL));

			        tailg  = mkg  g;
			        headg  = mkg' g;

			        g::apply_nodes (fn h = g::apply_nodes (fn t = edgefn(t,h)) tailg) headg;

				mk_e((SUBGRAPH(fn _ = headg)) ! l);
			    };
		    esac;

		mk_e _ => ();
	    end;


	    mk_e vs;

	    g;
	};
end;

%%
%header (generic package dotgraph_lr_vals_g(
	   #
           package token:  Token;
           package g : Attributed_Subgraph_Tree;
	   #
           graph_info:  Void -> g::Graph_Info;
           node_info :  Void -> g::Node_Info;
           edge_info :  Void -> g::Edge_Info;
         )
        )
%pos Int
%name Graph
%verbose
# %pure
%noshift EOF
%eop EOF
%term
    GRAPH | DIGRAPH | SUBGRAPH | STRICT
  | NODE | EDGE | EDGEOP | SYMBOL of String
  | COLON | SEMICOLON | COMMA | LBRACE | LBRACKET | LPAREN 
  | RBRACE | RBRACKET | RPAREN | EQUAL | DOT | AT | EOF
%nonterm
    file of Null_Or( g::Attributed_Graph )
  | graph_type of String
  | attr_ilk of (List ((String, String)) -> g::Attributed_Graph -> g::Attributed_Graph)
  | inside_attr_list of List( (String, String))
  | optcomma
  | attr_list of List( (String, String))
  | rec_attr_list of List( (String, String))
  | opt_attr_list of List( (String, String))
  | attr_set of (String, String)
  | stmt_list of  (g::Attributed_Graph -> g::Attributed_Graph)
  | stmt_list1 of (g::Attributed_Graph -> g::Attributed_Graph)
  | stmt of       (g::Attributed_Graph -> g::Attributed_Graph)
  | stmt1 of      (g::Attributed_Graph -> g::Attributed_Graph)
  | attr_stmt of  (g::Attributed_Graph -> g::Attributed_Graph)
  | node_id of (String, Null_Or( String ))
  | node_name of String
  | node_port of Null_Or( String )
  | port_location of String
  | port_angle of String
  | node_stmt of  (g::Attributed_Graph -> g::Attributed_Graph)
  | edge_stmt of  (g::Attributed_Graph -> g::Attributed_Graph)
  | edge_rhs of List( Vertex )
  | subg_stmt of  (g::Attributed_Graph -> g::Attributed_Graph)
  | subg_hdr of String
%left SUBGRAPH  # to avoid subgraph hdr shift/reduce conflict
%left LBRACE

%%

file            :    graph_type SYMBOL LBRACE stmt_list RBRACE 
                      ( { g = g::make_graph{ name=>symbol, graph_info, edge_info, node_info, info=>NULL};

                         g::set_attr (g::GRAPH_PART g) ("graph_type",graph_type);
                         THE (stmt_list g) ;
                       })
                |    /* empty */ (NULL)

graph_type      :    GRAPH          ("g")
                |    STRICT GRAPH   ("sg")
                |    DIGRAPH        ("dg")
                |    STRICT DIGRAPH ("sdg")

attr_ilk        :    GRAPH (fn al = fn g = { apply (ga_fn  g) al;  g; })
                |    NODE  (fn al = fn g = { apply (pna_fn g) al;  g; })
                |    EDGE  (fn al = fn g = { apply (pea_fn g) al;  g; })

inside_attr_list:    attr_set optcomma inside_attr_list 
		     (attr_set ! inside_attr_list)
                |    /* empty */ ([])

optcomma        :    /* empty */ ()
                |    COMMA ()

attr_list       :    LBRACKET inside_attr_list RBRACKET (inside_attr_list)

rec_attr_list   :    rec_attr_list attr_list (rec_attr_list @ attr_list)
                |    /* empty */ ([])

opt_attr_list   :    rec_attr_list (rec_attr_list)

attr_set        :    SYMBOL EQUAL SYMBOL  ((symbol1, symbol2))

stmt_list       :    stmt_list1 (stmt_list1)
                |    /* empty */ (fn g = g)

stmt_list1      :    stmt (stmt)
                |    stmt_list1 stmt (stmt o stmt_list1)

stmt            :    stmt1 (stmt1)
                |    stmt1 SEMICOLON (stmt1)

stmt1           :    node_stmt (node_stmt)
                |    edge_stmt (edge_stmt)
                |    attr_stmt (attr_stmt)
                |    subg_stmt (fn g = { subg_stmt g; g; })

attr_stmt       :    attr_ilk attr_list (attr_ilk attr_list)
                |    attr_set 
                      (fn g = { ga_fn g (#1 attr_set,#2 attr_set); g; })

node_id         :    node_name node_port (node_name, node_port)

node_name       :    SYMBOL (symbol)

node_port       :    /* empty */ (NULL)
                |    port_location (THE port_location)
                |    port_angle                        /* undocumented */
                      (THE port_angle)
                |    port_angle port_location            /* undocumented */
                      (THE (port_angle + port_location))
                |    port_location port_angle            /* undocumented */
                      (THE (port_location + port_angle))

port_location   :    COLON SYMBOL (":" + symbol)
                |    COLON LPAREN SYMBOL COMMA SYMBOL RPAREN 
                      (cat [":(",symbol1,",",symbol2,")"])
port_angle      :    AT SYMBOL ("@" + symbol)

node_stmt       :    node_id opt_attr_list 
                      (fn g = { apply (na_fn (g::get_node(g, #1 node_id,NULL))) opt_attr_list; g; })

edge_stmt       :    node_id edge_rhs opt_attr_list 
		     (make_edges((NODE node_id) ! edge_rhs, opt_attr_list))
                |    subg_stmt edge_rhs opt_attr_list
		     (make_edges((SUBGRAPH subg_stmt) ! edge_rhs, opt_attr_list))
edge_rhs        :    EDGEOP node_id ([NODE node_id])
	        |    EDGEOP node_id edge_rhs ((NODE node_id) ! edge_rhs)
                |    EDGEOP subg_stmt ([SUBGRAPH subg_stmt])
		|    EDGEOP subg_stmt edge_rhs ((SUBGRAPH subg_stmt) ! edge_rhs)

subg_stmt       :    subg_hdr LBRACE stmt_list RBRACE %prec LBRACE 
                      (fn g = (stmt_list (g::add_subgraph(g, subg_hdr,NULL))))
                |    LBRACE stmt_list RBRACE
                      (fn g = (stmt_list (g::add_subgraph(g,anonymous(),NULL))))
                |    subg_hdr %prec SUBGRAPH 
                      (fn g = find_subgraph(g, subg_hdr))

subg_hdr        :    SUBGRAPH SYMBOL (symbol)

