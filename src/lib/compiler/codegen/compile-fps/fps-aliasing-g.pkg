## fps-aliasing-g.pkg

# Compiled by:
#     src/lib/compiler/core.make6

# We are nowhere invoked.

generic package fps_aliasing_g (

    package cpsregions:  Fps_Region;		# Fps_Region	is from   src/lib/compiler/codegen/compile-fps/fps-regions.api
    exnptr:    Int;
    allocptr:  Int;
)
{
    package region= cpsregions;			# cpsregions	is from   src/lib/compiler/codegen/compile-fps/fps-regions.pkg
    package r = region;

    Reg = Int;
    My_Def_Use = 
       { m_def: List( Reg ), m_use: List( Reg ), r_def: List( Reg ), r_use: List( Reg ) };

    rw        = [65];
    ro        = [66];
    stack     = [67]; 
    real      = [68];
    storelist = [69]; 

    fun read_region r::RW_MEM   => { m_def=> [], m_use=> [],  r_def=> [], r_use=>rw };
	read_region r::RO_MEM   => { m_def=> [], m_use=> [],  r_def=> [], r_use=>ro };
	read_region r::STACK    => { m_def=> [], m_use=> [],  r_def=> [], r_use=>stack };
	read_region r::REAL     => { m_def=> [], m_use=> [],  r_def=> [], r_use=>real };
	read_region (r::RVAR r) => { m_def=> [], m_use=> [r], r_def=> [], r_use=> [] };
	read_region r::STORELIST => { m_def=> [], m_use=> [], r_def=> [], r_use=>storelist };
	read_region (r::MUTABLE (defs, uses)) => read_region uses;
	read_region (r::RECORD [(d, u, _)])
	 =>
	 {   my { m_def=>a, m_use=>b, r_def=>c, r_use=>d } =   read_region d;
	     my { m_def=>e, m_use=>f, r_def=>g, r_use=>h } =   write_region u;

	    {   m_def=>a@e,   m_use=>b@f,
		r_def=>c@g,   r_use=>d@h
	    };
	 };

	read_region (r::REGIONS (x, y))
	 =>
	 {   my { m_def=>a, m_use=>b, r_def=>c, r_use=>d } =   read_region x;
	     my { m_def=>e, m_use=>f, r_def=>g, r_use=>h } =   read_region y;

	     {   m_def=>a@e,   m_use=>b@f,
		 r_def=>c@g,   r_use=>d@h
	     };
	 };
    end 

   also
   fun write_region r::RW_MEM    => { m_def=> [],  m_use=> [], r_def=>rw,        r_use=> [] };
       write_region r::RO_MEM    => { m_def=> [],  m_use=> [], r_def=>ro,        r_use=> [] };
       write_region r::STACK     => { m_def=> [],  m_use=> [], r_def=>stack,     r_use=> [] };
       write_region r::REAL      => { m_def=> [],  m_use=> [], r_def=>real,      r_use=> [] };
       write_region (r::RVAR r)  => { m_def=> [r], m_use=> [], r_def=> [],       r_use=> [] };
       write_region r::STORELIST => { m_def=> [],  m_use=> [], r_def=>storelist, r_use=> [] };

       write_region (r::MUTABLE (defs, uses))
           =>
           write_region defs;

       write_region (r::RECORD [(d, u, _)])
	   =>
	   {   my { m_def=>a, m_use=>b, r_def=>c, r_use=>d } =   read_region d;
	       my { m_def=>e, m_use=>f, r_def=>g, r_use=>h } =   write_region u;

	      {   m_def=>a@e,   m_use=>b@f,
		  r_def=>c@g,   r_use=>d@h
	      };
	   };

       write_region (r::REGIONS (x, y))
	   =>
	   {   my { m_def=>a, m_use=>b, r_def=>c, r_use=>d } =   write_region x;
	       my { m_def=>e, m_use=>f, r_def=>g, r_use=>h } =   write_region y;

	       {   m_def=>a@e,   m_use=>b@f,
		   r_def=>c@g,   r_use=>d@h
	       };
	   };
    end;

    fun is_safe_read  r::RW_MEM              => FALSE;
	is_safe_read  r::RO_MEM              => FALSE;
	is_safe_read  r::STACK               => TRUE;
	is_safe_read  r::REAL                => TRUE;
	is_safe_read (r::RVAR r)             => FALSE;
	is_safe_read  r::STORELIST           => FALSE;
	is_safe_read (r::MUTABLE _)          => FALSE;
	is_safe_read (r::RECORD [(d, u, _)]) => is_safe_read d; 
	is_safe_read (r::REGIONS (a, b))     => is_safe_read a and is_safe_read b;
	is_safe_read  _                      => FALSE;
    end;

    fun is_trap_safe_read r::RW_MEM           => FALSE;
	is_trap_safe_read r::RO_MEM           => TRUE;
	is_trap_safe_read r::STACK            => FALSE;
	is_trap_safe_read r::REAL             => TRUE;
	is_trap_safe_read (r::RVAR r)         => TRUE;
	is_trap_safe_read r::STORELIST        => FALSE;
	is_trap_safe_read (r::MUTABLE _)      => FALSE;
	is_trap_safe_read (r::RECORD [(d, u, _)]) => is_trap_safe_read d; 
	is_trap_safe_read (r::REGIONS (a, b))   => is_trap_safe_read a 
				     and is_trap_safe_read b;
	is_trap_safe_read _                  => FALSE;
    end;

    fun is_safe_write r::RW_MEM           => FALSE;
	is_safe_write r::RO_MEM           => TRUE;
	is_safe_write r::STACK            => FALSE;
	is_safe_write r::REAL             => TRUE;
	is_safe_write (r::RVAR r)         => TRUE;
	is_safe_write r::STORELIST        => FALSE;
	is_safe_write (r::MUTABLE _)      => FALSE;
	is_safe_write (r::RECORD [(d, u, _)]) => is_safe_write d; 
	is_safe_write (r::REGIONS (a, b))   => is_safe_write a and is_safe_write b;
	is_safe_write _                  => FALSE;
    end;

    fun is_trap_safe_write mem
	=
	is_safe_write mem;

    trap_barrier = {  def   => [allocptr],
			uses  => [allocptr],
			m_def => [],
			m_use => [],
			r_use => [],
			r_def => []
		     };

};

