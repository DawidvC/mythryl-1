## calc-app.pkg

# Compiled by:
#     src/lib/x-kit/tut/calc/calc-app.make6


# This is a test driver for the calculator.


stipulate
    include threadkit;					# threadkit		is from   src/lib/src/lib/thread-kit/src/core-thread-kit/threadkit.pkg
    #
    package rx  =  run_xkit;				# run_xkit		is from   src/lib/x-kit/widget/lib/run-xkit.pkg
    package tw  =  topwindow;				# topwindow		is from   src/lib/x-kit/widget/basic/topwindow.pkg
    package w   =  widget;				# widget		is from   src/lib/x-kit/widget/basic/widget.pkg
    package wa  =  widget_attribute;			# widget_attribute	is from   src/lib/x-kit/widget/lib/widget-attribute.pkg
    package wy  =  widget_style;			# widget_style		is from   src/lib/x-kit/widget/lib/widget-style.pkg
    #
    package cal =  calc;				# calc			is from   src/lib/x-kit/tut/calc/calc.pkg
herein

    package calc_app {

	stipulate

	    resources
		=
		[ "*background: gray" ];

	    fun tester root
		=
		{   fun quit ()
			=
			{   w::delete_root_window root;
			    shut_down_thread_scheduler  winix::process::success;
			};

		    style = w::style_from_strings (root, resources);

		    name = wy::make_view
			     {
			       name    =>   wy::style_name [],
			       aliases => [ wy::style_name [] ]
			     };

		    view = (name, style);

		    calc = cal::make_calc (root, view, []);

		    topwindow_args
			=
			[ (wa::title,      wa::STRING_VAL "calc"),
			  (wa::icon_name,  wa::STRING_VAL "calc")
			];

		    topwindow = tw::topwindow  (root, view, topwindow_args)  calc;

		    tw::start  topwindow;
		};
	herein

	    fun do_it' (debug_flags, server)
		=
		{   xtracing::init debug_flags;

		    rx::run_xkit'
			tester
			{ display => THE server,
			  time_quantum_in_milliseconds  => NULL
			};
		};

	    fun do_it ()
		=
		rx::run_xkit  tester;

	    fun main (program, server ! _) =>  do_it' ([], server);
		main _                     =>  do_it  ();
	    end;

	end;			# stipulate
    };				# package calc_test 
end;

## COPYRIGHT (c) 1991 by AT&T Bell Laboratories.  See COPYRIGHT file for details.
## Subsequent changes by Jeff Prothero Copyright (c) 2010,
## released under Gnu Public Licence version 3.
