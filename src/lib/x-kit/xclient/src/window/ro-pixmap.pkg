## ro-pixmap.pkg
#
# See also:
#     src/lib/x-kit/xclient/src/window/window.pkg
#     src/lib/x-kit/xclient/src/window/cs-pixmap.pkg
#     src/lib/x-kit/xclient/src/window/rw-pixmap.pkg

# Compiled by:
#     src/lib/x-kit/xclient/xclient-internals.make6





###                     "My method to overcome a difficulty is to go round it."
###
###                                               -- George Polya


stipulate
    package cw =  cs_pixmap;
    package d  =  draw;						# draw		is from   src/lib/x-kit/xclient/src/window/draw.pkg
    package dt =  draw_types;					# draw_types	is from   src/lib/x-kit/xclient/src/window/draw-types.pkg
    package g  =  geometry;					# geometry	is from   src/lib/x-kit/xclient/src/window/geometry.pkg
    package wp =  rw_pixmap;					# rw_pixmap	is from   src/lib/x-kit/xclient/src/window/rw-pixmap.pkg
    package sn =  xsession;					# xsession	is from   src/lib/x-kit/xclient/src/window/xsession.pkg
herein

    package ro_pixmap: (weak)  Ro_Pixmap {			# Ro_Pixmap	is from   src/lib/x-kit/xclient/src/window/ro-pixmap.api

	stipulate
	    package d: (weak)  api { Ro_Pixmap =  RO_PIXMAP dt::Rw_Pixmap; }
                =
                draw_types;
        herein
            include d;
	end;


	fun make_ro_pixmap_from_cs_pixmap screen im
	    =
	    dt::RO_PIXMAP (cw::make_rw_pixmap_from_cs_pixmap screen im);


	fun make_ro_pixmap_from_ascii screen data
	    =
	    dt::RO_PIXMAP (cw::make_rw_pixmap_from_ascii_data screen data);


	fun make_ro_pixmap_from_rw_pixmap (pm as dt::RW_PIXMAP { screen, size, screen_depth, ... } )
	    =
	    {   screen_depth
		    ->
		    sn::SCREEN_DEPTH_IMPS { depth, ... };

		new_pixmap
		    =
		    wp::make_rw_pixmap
			screen
			(size, depth);

		d::pixel_blt
		    #
		    (dt::drawable_of_rw_pixmap  new_pixmap)
		    #
		    pen::default_pen
		    #
		    { from     =>  dt::FROM_RW_PIXMAP pm,
		      from_box =>  g::make_box (g::point_00, size),
		      to_pos   =>  g::point_00
		    };

		dt::RO_PIXMAP new_pixmap;
	    };
    };
end;

## COPYRIGHT (c) 1990, 1991 by John H. Reppy.  See COPYRIGHT file for details.
## Subsequent changes by Jeff Prothero Copyright (c) 2010,
## released under Gnu Public Licence version 3.
