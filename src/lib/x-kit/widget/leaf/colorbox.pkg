## colorbox.pkg
#
# Widget that fills box with a color.

# Compiled by:
#     src/lib/x-kit/widget/xkit-widget.make6





###      "Who the hell wants to hear actors talk?"
###               --  H M Warner
###                   of Warner Brothers, 1927 


stipulate
    package w  =  widget;				# widget		is from   src/lib/x-kit/widget/basic/widget.pkg
    package wa =  widget_attribute;			# widget_attribute	is from   src/lib/x-kit/widget/util/attribute.pkg
    #
    package xc =  xcore;				# xcore			is from   src/lib/x-kit/xclient/xcore.pkg
    package xi =  xinput;				# xinput		is from   src/lib/x-kit/xclient/xinput.pkg
herein

    package colorbox: (weak)  Colorbox {		# Colorbox		is from   src/lib/x-kit/widget/leaf/colorbox.api

	attributes
	    =
	    [  (wa::foreground,     wa::COLOR,    wa::STRING_VAL "black")  ];

	fun crect (root_window, color, size_preference_thunk_of)
	    =
	    {   fun realize { kidplug, window, size }
		    =
		    {   xi::ignore_all  kidplug;
			();
		    };

		w::make_widget
		  {
		    root_window, 
		    args =>   fn () = { background => THE color },
		    size_preference_thunk_of,
		    realize
		  };
	    };

	fun colorbox  (root_window, view, args)  size_preference_thunk_of
	    =
	    {   attributes = w::find_attribute (w::attributes (view, attributes, args));

		color = wa::get_color (attributes wa::foreground);

		crect (root_window, color, size_preference_thunk_of);
	    };

	fun make_colorbox  root_window  (color_opt, size_preference_thunk_of)
	    =
	    {   screen = w::screen_of root_window;

		color = case color_opt   
			    #
			    THE color =>  color;
			    NULL      =>  xc::rgb_from_floats (0.0, 0.0, 0.0);
			esac;

		crect (root_window, color, size_preference_thunk_of);
	    };

    };		# package colored_box 

end;


## COPYRIGHT (c) 1994 by AT&T Bell Laboratories  See COPYRIGHT file for details.
## Subsequent changes by Jeff Prothero Copyright (c) 2010,
## released under Gnu Public Licence version 3.
